%define api.prefix {das_yy}
%code requires {
    #include "daScript/misc/platform.h"
    #include "daScript/ast/ast.h"
    #include "daScript/ast/ast_generate.h"
    #include "daScript/ast/ast_expressions.h"

    enum {
        CorM_COPY   = 0,
        CorM_MOVE   = (1<<0),
        CorM_CLONE  = (1<<1)
    };

    namespace das {
        extern ProgramPtr               g_Program;
        extern FileAccessPtr            g_Access;
        extern vector<FileInfo *>       g_FileAccessStack;
        extern das_map<string,string>   das_module_alias;

        extern ReaderMacro *            g_ReaderMacro;
        extern ExprReader *             g_ReaderExpr;
    }
    using namespace das;

    struct VariableDeclaration {
        VariableDeclaration ( vector<pair<string,LineInfo>> * n, TypeDecl * t, Expression * i )
            : pNameList(n), pTypeDecl(t), pInit(i) {}
        virtual ~VariableDeclaration () {
            if ( pNameList ) delete pNameList;
            delete pTypeDecl;
            if ( pInit ) delete pInit;
            if ( annotation ) delete annotation;
        }
        vector<pair<string,LineInfo>>   *pNameList;
        TypeDecl                *pTypeDecl;
        Expression              *pInit;
        bool                    init_via_move = false;
        bool                    init_via_clone = false;
        bool                    override = false;
        AnnotationArgumentList  *annotation = nullptr;
    };

    ExprLooksLikeCall * parseFunctionArguments ( ExprLooksLikeCall * pCall, Expression * arguments );
    vector<ExpressionPtr> sequenceToList ( Expression * arguments );
    void deleteVariableDeclarationList ( vector<VariableDeclaration *> * list );
    void varDeclToTypeDecl ( TypeDecl * pType, vector<VariableDeclaration*> * list, bool needNames = true );
    void runFunctionAnnotations ( Function * func, AnnotationList * annL, const LineInfo & at );

    LineInfo tokAt ( const struct YYLTYPE & li );
    LineInfo tokRangeAt ( const struct YYLTYPE & li, const struct YYLTYPE & lie );

    Annotation * findAnnotation ( const string & name, const LineInfo & at );

    extern bool das_need_oxford_comma;
    extern bool das_force_oxford_comma;
    extern bool das_supress_errors;
    extern int das_arrow_depth;

    extern Structure * g_thisStructure;
}

%{
    #include "daScript/misc/platform.h"
    #include "daScript/simulate/debug_info.h"
    #include "daScript/ast/compilation_errors.h"

    #ifdef _MSC_VER
    #pragma warning(disable:4262)
    #pragma warning(disable:4127)
    #pragma warning(disable:4702)
    #endif

    using namespace das;

    void das_yyerror (const string & error);
    void das_yyfatalerror (const string & error, das::CompilationError cerr = das::CompilationError::syntax_error);
    void das_yyerror (const string & error, const das::LineInfo & at, das::CompilationError cerr = das::CompilationError::unspecified);
    void das_checkName(const string & name, const LineInfo &at);
    int yylex();
    void yybegin(const char * str);

    void das_yybegin_reader ( void );
    void das_yyend_reader ( void );

    __forceinline string inThisModule ( const string & name ) { return "_::" + name; }
%}

%locations
%verbose
%debug

%define parse.error verbose

%union {
    char                            ch;
    bool                            b;
    int32_t                         i;
    uint32_t                        ui;
    int64_t                         i64;
    uint64_t                        ui64;
    double                          d;
    double                          fd;
    string *                        s;
    vector<string> *                pNameList;
    vector<pair<string,LineInfo>> * pNameWithPosList;
    VariableDeclaration *           pVarDecl;
    vector<VariableDeclaration*> *  pVarDeclList;
    TypeDecl *                      pTypeDecl;
    Expression *                    pExpression;
    Type                            type;
    AnnotationArgument *            aa;
    AnnotationArgumentList *        aaList;
    AnnotationDeclaration *         fa;
    AnnotationList *                faList;
    MakeStruct *                    pMakeStruct;
    Enumeration *                   pEnum;
    Structure *                     pStructure;
    Function *                      pFuncDecl;
    CaptureEntry *                  pCapt;
    vector<CaptureEntry> *          pCaptList;
}

%destructor { delete $$; } <s>
%destructor { delete $$; } <pNameList>
%destructor { delete $$; } <pNameWithPosList>
%destructor { delete $$; } <pVarDecl>
%destructor { deleteVariableDeclarationList($$); } <pVarDeclList>
%destructor { delete $$; } <pTypeDecl>
%destructor { delete $$; } <pExpression>
%destructor { delete $$; } <aa>
%destructor { delete $$; } <aaList>
%destructor { delete $$; } <fa>
%destructor { delete $$; } <faList>
%destructor { delete $$; } <pMakeStruct>
%destructor { delete $$; } <pEnum>
%destructor { delete $$; } <pCapt>
%destructor { delete $$; } <pCaptList>

%token  LEXER_ERROR         "lexer error"

/* keywords */
%token  DAS_STRUCT          "struct"
%token  DAS_CLASS           "class"
%token  DAS_LET             "let"
%token  DAS_DEF             "def"
%token  DAS_WHILE           "while"
%token  DAS_IF              "if"
%token  DAS_STATIC_IF       "static_if"
%token  DAS_ELSE            "else"
%token  DAS_FOR             "for"
%token  DAS_CATCH           "recover"
%token  DAS_TRUE            "true"
%token  DAS_FALSE           "false"
%token  DAS_NEWT            "new"
%token  DAS_TYPEINFO        "typeinfo"
%token  DAS_TYPE            "type"
%token  DAS_IN              "in"
%token  DAS_IS              "is"
%token  DAS_AS              "as"
%token  DAS_ELIF            "elif"
%token  DAS_STATIC_ELIF     "static_elif"
%token  DAS_ARRAY           "array"
%token  DAS_RETURN          "return"
%token  DAS_NULL            "null"
%token  DAS_BREAK           "break"
%token  DAS_TRY             "try"
%token  DAS_OPTIONS         "options"
%token  DAS_TABLE           "table"
%token  DAS_EXPECT          "expect"
%token  DAS_CONST           "const"
%token  DAS_REQUIRE         "require"
%token  DAS_OPERATOR        "operator"
%token  DAS_ENUM            "enum"
%token  DAS_FINALLY         "finally"
%token  DAS_DELETE          "delete"
%token  DAS_DEREF           "deref"
%token  DAS_TYPEDEF         "typedef"
%token  DAS_WITH            "with"
%token  DAS_CAST            "cast"
%token  DAS_OVERRIDE        "override"
%token  DAS_ABSTRACT        "abstract"
%token  DAS_UPCAST          "upcast"
%token  DAS_ITERATOR        "iterator"
%token  DAS_VAR             "var"
%token  DAS_ADDR            "addr"
%token  DAS_CONTINUE        "continue"
%token  DAS_WHERE           "where"
%token  DAS_PASS            "pass"
%token  DAS_REINTERPRET     "reinterpret"
%token  DAS_MODULE          "module"
%token  DAS_PUBLIC          "public"
%token  DAS_LABEL           "label"
%token  DAS_GOTO            "goto"
%token  DAS_IMPLICIT        "implicit"
%token  DAS_EXPLICIT        "explicit"
%token  DAS_SHARED          "shared"
%token  DAS_PRIVATE         "private"
%token  DAS_SMART_PTR       "smart_ptr"
%token  DAS_UNSAFE          "unsafe"

/* basic types */
%token  DAS_TBOOL           "bool"
%token  DAS_TVOID           "void"
%token  DAS_TSTRING         "string"
%token  DAS_TAUTO           "auto"
%token  DAS_TINT            "int"
%token  DAS_TINT2           "int2"
%token  DAS_TINT3           "int3"
%token  DAS_TINT4           "int4"
%token  DAS_TUINT           "uint"
%token  DAS_TBITFIELD       "bitfield"
%token  DAS_TUINT2          "uint2"
%token  DAS_TUINT3          "uint3"
%token  DAS_TUINT4          "uint4"
%token  DAS_TFLOAT          "float"
%token  DAS_TFLOAT2         "float2"
%token  DAS_TFLOAT3         "float3"
%token  DAS_TFLOAT4         "float4"
%token  DAS_TRANGE          "range"
%token  DAS_TURANGE         "urange"
%token  DAS_TBLOCK          "block"
%token  DAS_TINT64          "int64"
%token  DAS_TUINT64         "uint64"
%token  DAS_TDOUBLE         "double"
%token  DAS_TFUNCTION       "function"
%token  DAS_TLAMBDA         "lambda"
%token  DAS_TINT8           "int8"
%token  DAS_TUINT8          "uint8"
%token  DAS_TINT16          "int16"
%token  DAS_TUINT16         "uint16"
%token  DAS_TTUPLE          "tuple"
%token  DAS_TVARIANT        "variant"
%token  DAS_GENERATOR       "generator"
%token  DAS_YIELD           "yield"

/* operators */
%token  ADDEQU              "+="
%token  SUBEQU              "-="
%token  DIVEQU              "/="
%token  MULEQU              "*="
%token  MODEQU              "%="
%token  ANDEQU              "&="
%token  OREQU               "|="
%token  XOREQU              "^="
%token  SHL                 "<<"
%token  SHR                 ">>"
%token  ADDADD              "++"
%token  SUBSUB              "--"
%token  LEEQU               "<="
%token  SHLEQU              "<<="
%token  SHREQU              ">>="
%token  GREQU               ">="
%token  EQUEQU              "=="
%token  NOTEQU              "!="
%token  RARROW              "->"
%token  LARROW              "<-"
%token  QQ                  "??"
%token  QDOT                "?."
%token  QBRA                "?["
%token  LPIPE               "<|"
%token  LBPIPE              " <|"
%token  LAPIPE              "@ <|"
%token  LFPIPE              "@@ <|"
%token  RPIPE               "|>"
%token  CLONEEQU            ":="
%token  ROTL                "<<<"
%token  ROTR                ">>>"
%token  ROTLEQU             "<<<="
%token  ROTREQU             ">>>="
%token  MAPTO               "=>"
%token  COLCOL              "::"
%token  ANDAND              "&&"
%token  OROR                "||"
%token  XORXOR              "^^"
%token  ANDANDEQU           "&&="
%token  OROREQU             "||="
%token  XORXOREQU           "^^="


%token  BRABRAB             "[["    /* [[ */
%token  BRACBRB             "[{"    /* [{ */
%token  CBRCBRB             "{{"    /* {{ */

%token<i>       INTEGER                 "integer constant"
%token<i64>     LONG_INTEGER            "long integer constant"
%token<ui>      UNSIGNED_INTEGER        "unsigned integer constant"
%token<ui64>    UNSIGNED_LONG_INTEGER   "unsigned long integer constant"
%token<fd>      FLOAT                   "floating point constant"
%token<d>       DOUBLE                  "double constant"
%token<s>       NAME                    "name"

/* %printer { fprintf (yyo, "%i", $$); } <i> */

%token      BEGIN_STRING            "start of the string"
%token<ch>  STRING_CHARACTER
%token      END_STRING              "end of the string"
%token      BEGIN_STRING_EXPR       "{"
%token      END_STRING_EXPR         "}"
%token      END_OF_READ             "end of failed eader macro"

%type <pNameList>       bitfield_bits
%type <pNameWithPosList>variable_name_with_pos_list
%type <type>            basic_type_declaration
%type <type>            enum_basic_type_declaration
%type <pTypeDecl>       type_declaration
%type <pTypeDecl>       auto_type_declaration
%type <pTypeDecl>       bitfield_type_declaration
%type <pTypeDecl>       optional_function_type
%type <pTypeDecl>       structure_type_declaration
%type <pVarDeclList>    struct_variable_declaration_list
%type <pVarDeclList>    global_variable_declaration_list
%type <pVarDeclList>    optional_function_argument_list
%type <pVarDeclList>    function_argument_list
%type <pVarDecl>        variable_declaration
%type <pVarDecl>        function_argument_declaration
%type <pVarDecl>        structure_variable_declaration
%type <pVarDecl>        let_variable_declaration
%type <pVarDecl>        tuple_type
%type <pVarDeclList>    tuple_type_list
%type <pVarDecl>        variant_type
%type <pVarDeclList>    variant_type_list
%type <s>               name_in_namespace
%type <s>               annotation_declaration_name
%type <s>               function_name
%type <s>               optional_structure_parent
%type <s>               require_module_name
%type <aa>              annotation_argument
%type <aa>              annotation_argument_value
%type <aaList>          annotation_argument_list
%type <aaList>          annotation_argument_value_list
%type <aaList>          optional_field_annotation
%type <fa>              annotation_declaration
%type <faList>          annotation_list
%type <faList>          optional_annotation_list
%type <pStructure>      structure_name

%type <b>               class_or_struct
%type <b>               copy_or_move
%type <b>               optional_override
%type <b>               kwd_let
%type <b>               is_public_module
%type <b>               optional_shared
%type <i>               optional_shared_or_private
%type <i>               block_or_lambda
%type <b>               optional_ref

%type <b>               if_or_static_if
%type <b>               elif_or_static_elif

%type <i>               copy_or_move_or_clone

%type <pEnum>           enum_list

%type <pExpression>     expr
%type <pExpression>     expr_field
%type <pExpression>     expr_assign
%type <pExpression>     expr_assign_pipe
%type <pExpression>     expr_list
%type <pExpression>     expression_label
%type <pExpression>     expression_goto
%type <pExpression>     expression_let
%type <pExpression>     expressions
%type <pExpression>     expression_block
%type <pExpression>     expression_with
%type <pExpression>     expression_while_loop
%type <pExpression>     expression_unsafe
%type <pExpression>     expression_for_loop
%type <pExpression>     expression_break
%type <pExpression>     expression_continue
%type <pExpression>     expression_delete
%type <pExpression>     expression_return
%type <pExpression>     expression_yield
%type <pExpression>     expression_else
%type <pExpression>     expression_if_then_else
%type <pExpression>     expression_try_catch
%type <pExpression>     expr_new
%type <pExpression>     expr_pipe
%type <pExpression>     expr_type_info
%type <pExpression>     expr_type_decl
%type <pExpression>     expr_cast
%type <pExpression>     expr_block
%type <pExpression>     expr_named_call
%type <pExpression>     expression_any
%type <pExpression>     expr_numeric_const
%type <pExpression>     string_builder_body
%type <pExpression>     string_builder
%type <pExpression>     expr_reader
%type <pExpression>     make_decl
%type <pMakeStruct>     make_struct_fields
%type <pExpression>     make_struct_decl
%type <pExpression>     make_struct_dim
%type <pExpression>     make_tuple
%type <pExpression>     make_map_tuple
%type <pExpression>     make_any_tuple
%type <pExpression>     make_dim
%type <pExpression>     make_dim_decl
%type <pExpression>     make_table
%type <pExpression>     make_table_decl
%type <pExpression>     array_comprehension_where
%type <pExpression>     array_comprehension
%type <pExpression>     expr_method_call
%type <pExpression>     block_or_simple_block
%type <pExpression>     func_addr_expr

%type <pExpression>     optional_block

%type <pFuncDecl>       function_declaration
%type <pFuncDecl>       function_declaration_header

%type <pCapt>           capture_entry
%type <pCaptList>       capture_list
%type <pCaptList>       optional_capture_list

%type <s>               character_sequence
%type <s>               string_constant

/* operation precedence*/
%left       ','
%nonassoc    MAPTO
%right      '=' ADDEQU SUBEQU DIVEQU MULEQU MODEQU ANDEQU OREQU XOREQU LARROW SHLEQU SHREQU ROTLEQU ROTREQU ANDANDEQU XORXOREQU OROREQU
%right      '?' ':'
%left       OROR
%left       XORXOR
%left       ANDAND
%left       '|'
%left       '^'
%left       '&'
%left       EQUEQU NOTEQU
%left       '<' '>' LEEQU GREQU
%left       SHL SHR ROTL ROTR
%left       '-' '+'
%left       '*' '/' '%'
%right      QQ
%right      UNARY_MINUS UNARY_PLUS '~' '!' PRE_INC PRE_DEC ADDADD SUBSUB
%left       DAS_IS DAS_AS
%left       LPIPE RPIPE LBPIPE LAPIPE
%left       POST_INC POST_DEC RARROW DEREF
%left       '.'
%nonassoc  '[' ']' '(' ')' QDOT QBRA BRABRAB BRACBRB CBRCBRB
%nonassoc   '$' '@'
%nonassoc   COLCOL

%%

program
    :
    |   program module_declaration
    |   program structure_declaration
    |   program enum_declaration
    |   program global_let
    |   program global_function_declaration
    |   program expect_declaration
    |   program require_declaration
    |   program options_declaration
    |   program alias_declaration
    |   program variant_alias_declaration
    |   program bitfield_alias_declaration
    |   program ';'
    ;

module_declaration
    :   DAS_MODULE  NAME[name] optional_shared[mtype] {
        if ( g_Program->thisModule->name.empty() ) {
            g_Program->thisModule->name = *$name;
        } else if ( g_Program->thisModule->name != *$name ){
            das_yyerror("this module already has a name, and its " + g_Program->thisModule->name,tokAt(@name),
                CompilationError::module_already_has_a_name);
        }
        if ( !g_Program->policies.ignore_shared_modules ) {
            g_Program->promoteToBuiltin = $mtype;
        }
        delete $name;
    }
    ;

character_sequence
    :   STRING_CHARACTER[char]                              { $$ = new string(); *$$ += $char; }
    |   character_sequence[elem]  STRING_CHARACTER[char]    { $$ = $elem; *$elem += $char; }
    ;

string_constant
    :   BEGIN_STRING character_sequence[seq] END_STRING    { $$ = $seq; }
    ;

string_builder_body
    :   {
        $$ = new ExprStringBuilder();
        $$->at = LineInfo(g_FileAccessStack.back(),
            yylloc.first_column,yylloc.first_line,yylloc.last_column,yylloc.last_line);
    }
    |   string_builder_body[sb] character_sequence[sconst] {
        bool err;
        auto esconst = unescapeString(*$sconst,&err);
        if ( err ) das_yyerror("invalid escape sequence",tokAt(@sb), CompilationError::invalid_escape_sequence);
        auto sc = make_smart<ExprConstString>(tokAt(@sconst),esconst);
        delete $sconst;
        static_cast<ExprStringBuilder *>($sb)->elements.push_back(sc);
        $$ = $sb;
    }
    |   string_builder_body[sb] BEGIN_STRING_EXPR expr[subexpr] END_STRING_EXPR {
        auto se = ExpressionPtr($subexpr);
        static_cast<ExprStringBuilder *>($sb)->elements.push_back(se);
        $$ = $sb;
    }
    ;

string_builder
    :   BEGIN_STRING string_builder_body[sb] END_STRING {
        auto strb = static_cast<ExprStringBuilder *>($sb);
        if ( strb->elements.size()==0 ) {
            $$ = new ExprConstString(strb->at,"");
            delete $sb;
        } else if ( strb->elements.size()==1 && strb->elements[0]->rtti_isStringConstant() ) {
            auto sconst = static_pointer_cast<ExprConstString>(strb->elements[0]);
            $$ = new ExprConstString(strb->at,sconst->text);
            delete $sb;
        } else {
            $$ = $sb;
        }
    }
    ;

reader_character_sequence
    :   STRING_CHARACTER[char] {
        if ( !g_ReaderMacro->accept(g_Program.get(), g_Program->thisModule.get(), g_ReaderExpr, $char, tokAt(@char)) ) {
            das_yyend_reader();
        }
    }
    |   reader_character_sequence[elem]  STRING_CHARACTER[char] {
        if ( !g_ReaderMacro->accept(g_Program.get(), g_Program->thisModule.get(), g_ReaderExpr, $char, tokAt(@char)) ) {
            das_yyend_reader();
        }
    }
    ;

expr_reader
    : '%' [loc] name_in_namespace[mark] {
        auto macros = g_Program->getReaderMacro(*$mark);
        if ( macros.size()==0 ) {
            das_yyerror("reader macro " + *$mark + " not found",tokAt(@mark),
                CompilationError::unsupported_read_macro);
        } else if ( macros.size()>1 ) {
            string options;
            for ( auto & x : macros ) {
                options += "\t" + x->module->name + "::" + x->name + "\n";
            }
            das_yyerror("too many options for macro " + *$mark +  "\n" + options, tokAt(@mark),
                CompilationError::unsupported_read_macro);
        } else if ( yychar != '~' ) {
            das_yyerror("expecting ~ after the reader macro", tokAt(@mark),
                CompilationError::syntax_error);
        } else {
            g_ReaderMacro = macros.back().get();
            g_ReaderExpr = new ExprReader(tokAt(@loc),g_ReaderMacro);
            yyclearin ;
            das_yybegin_reader();
        }
    } reader_character_sequence[seq] {
        g_ReaderExpr->at = tokRangeAt(@loc,@seq);
        $$ = g_ReaderExpr;
        delete $mark;
        g_ReaderMacro = nullptr;
        g_ReaderExpr = nullptr;
    }

options_declaration
    :   DAS_OPTIONS annotation_argument_list[list] {
        if ( g_Program->options.size() ) {
            g_Program->options.insert ( g_Program->options.begin(),
                $list->begin(), $list->end() );
        } else {
            swap ( g_Program->options, *$list );
        }
        auto opt = g_Program->options.find("indenting", tInt);
        extern int das_tab_size, das_def_tab_size;
        if (opt)
        {
            if (opt->iValue != 0 && opt->iValue != 2 && opt->iValue != 4 && opt->iValue != 8)//this is error
                das_tab_size = das_def_tab_size;
            else
                das_tab_size = opt->iValue ? opt->iValue : das_def_tab_size;//0 is default
            g_FileAccessStack.back()->tabSize = das_tab_size;
        }
        delete $list;
    }
    ;

require_declaration
    :   DAS_REQUIRE require_module
    ;

require_module_name
    :   NAME[name] {
        $$ = $name;
    }
    |   require_module_name[req] '.' NAME[name] {
        *$req += ".";
        *$req += *$name;
        delete $name;
        $$ = $req;
    }
    |   require_module_name[req] '/' NAME[name] {
        *$req += "/";
        *$req += *$name;
        delete $name;
        $$ = $req;
    }
    ;

require_module
    :   require_module_name[name]  is_public_module[pub] {
        auto info = g_Access->getModuleInfo(*$name, g_FileAccessStack.back()->name);
        if ( auto mod = g_Program->addModule(info.moduleName) ) {
            g_Program->thisModule->addDependency(mod, $pub);
            if ( !info.importName.empty() ) {
                auto ita = das_module_alias.find(info.importName);
                if ( ita !=das_module_alias.end() ) {
                    if ( ita->second != info.moduleName ) {
                        das_yyerror("module alias already used " + info.importName + " as " + ita->second,tokAt(@name),
                            CompilationError::module_not_found);
                    }
                } else {
                    das_module_alias[info.importName] = info.moduleName;
                }
            }
        } else {
            das_yyerror("required module not found " + *$name,tokAt(@name),
                CompilationError::module_not_found);
        }
        delete $name;
    }
    |   require_module_name[name] DAS_AS NAME[modalias] is_public_module[pub] {
        auto info = g_Access->getModuleInfo(*$name, g_FileAccessStack.back()->name);
        if ( auto mod = g_Program->addModule(info.moduleName) ) {
            g_Program->thisModule->addDependency(mod, $pub);
            auto ita = das_module_alias.find(*$modalias);
            if ( ita !=das_module_alias.end() ) {
                if ( ita->second != info.moduleName ) {
                    das_yyerror("module alias already used " + *$modalias + " as " + ita->second,tokAt(@name),
                        CompilationError::module_not_found);
                }
            } else {
                das_module_alias[*$modalias] = info.moduleName;
            }
        } else {
            das_yyerror("required module not found " + *$name,tokAt(@name),
                CompilationError::module_not_found);
        }
        delete $name;
        delete $modalias;
    }
    ;

is_public_module
    :               { $$ = false; }
    |   DAS_PUBLIC  { $$ = true; }
    ;

expect_declaration
    :   DAS_EXPECT expect_list
    ;

expect_list
    :   expect_error
    |   expect_list ',' expect_error
    ;

expect_error
    : INTEGER[err] {
        g_Program->expectErrors[CompilationError($err)] ++;
    }
    | INTEGER[err] ':' INTEGER[count] {
        g_Program->expectErrors[CompilationError($err)] += $count;
    }
    ;

expression_label
    : DAS_LABEL[loc] INTEGER[labname] ':' {
        $$ = new ExprLabel(tokAt(@loc),$labname);
    }
    ;

expression_goto
    : DAS_GOTO[loc] DAS_LABEL INTEGER[labname]  {
        $$ = new ExprGoto(tokAt(@loc),$labname);
    }
    | DAS_GOTO[loc] expr[lab]  {
        $$ = new ExprGoto(tokAt(@loc),ExpressionPtr($lab));
    }
    ;

elif_or_static_elif
    :   DAS_ELIF          { $$ = false; }
    |   DAS_STATIC_ELIF   { $$ = true; }
    ;

expression_else
    :                                                      { $$ = nullptr; }
    |   DAS_ELSE expression_block[block]                   { $$ = $block; }
    |   elif_or_static_elif[loc] expr[cond] expression_block[block] expression_else[then] {
        auto eite = new ExprIfThenElse(tokAt(@loc),ExpressionPtr($cond),
            ExpressionPtr($block),ExpressionPtr($then));
        eite->isStatic = $loc;
        $$ = eite;
    }
    ;

if_or_static_if
    :   DAS_IF          { $$ = false; }
    |   DAS_STATIC_IF   { $$ = true; }
    ;

expression_if_then_else
    :   if_or_static_if[loc] expr[cond] expression_block[block] expression_else[then] {
        auto eite = new ExprIfThenElse(tokAt(@loc),ExpressionPtr($cond),
            ExpressionPtr($block),ExpressionPtr($then));
        eite->isStatic = $loc;
        $$ = eite;
    }
    ;

expression_for_loop
    :   DAS_FOR[loc] variable_name_with_pos_list[iters] DAS_IN expr_list[srcs] ';' expression_block[block] {
        auto pFor = new ExprFor(tokAt(@loc));
        pFor->visibility = tokRangeAt(@loc,@block);
        for ( const auto & np : *$iters ) {
            pFor->iterators.push_back(np.first);
            pFor->iteratorsAt.push_back(np.second);
        }
        delete $iters;
        pFor->sources = sequenceToList($srcs);
        pFor->body = ExpressionPtr($block);
        ((ExprBlock *)$block)->inTheLoop = true;
        $$ = pFor;
    }
    ;

expression_unsafe
    :   DAS_UNSAFE[loc] expression_block[block] {
        auto pUnsafe = new ExprUnsafe(tokAt(@loc));
        pUnsafe->body = ExpressionPtr($block);
        $$ = pUnsafe;
    }
    ;

expression_while_loop
    :   DAS_WHILE[loc] expr[condition] expression_block[block] {
        auto pWhile = new ExprWhile(tokAt(@loc));
        pWhile->cond = ExpressionPtr($condition);
        pWhile->body = ExpressionPtr($block);
        ((ExprBlock *)$block)->inTheLoop = true;
        $$ = pWhile;
    }
    ;

expression_with
    :   DAS_WITH[loc] expr[with] expression_block[block] {
        auto pWith = new ExprWith(tokAt(@loc));
        pWith->with = ExpressionPtr($with);
        pWith->body = ExpressionPtr($block);
        $$ = pWith;
    }
    ;

annotation_argument_value
    :   string_constant[value]   { $$ = new AnnotationArgument("",*$value); delete $value; }
    |   NAME[value]              { $$ = new AnnotationArgument("",*$value); delete $value; }
    |   INTEGER[value]           { $$ = new AnnotationArgument("",$value); }
    |   FLOAT[value]             { $$ = new AnnotationArgument("",float($value)); }
    |   DAS_TRUE                 { $$ = new AnnotationArgument("",true); }
    |   DAS_FALSE                { $$ = new AnnotationArgument("",false); }
    ;

annotation_argument_value_list
    :   annotation_argument_value[arg] {
        $$ = new AnnotationArgumentList();
        $$->push_back(*$arg);
        delete $arg;
    }
    |   annotation_argument_value_list[argL] ',' annotation_argument_value[arg] {
            $$ = $argL;
            $$->push_back(*$arg);
            delete $arg;
    }
    ;

annotation_argument
    :   NAME[name] '=' string_constant[value]   { $$ = new AnnotationArgument(*$name,*$value); delete $value; delete $name; }
    |   NAME[name] '=' NAME[value]              { $$ = new AnnotationArgument(*$name,*$value); delete $value; delete $name; }
    |   NAME[name] '=' INTEGER[value]           { $$ = new AnnotationArgument(*$name,$value); delete $name; }
    |   NAME[name] '=' FLOAT[value]             { $$ = new AnnotationArgument(*$name,float($value)); delete $name; }
    |   NAME[name] '=' DAS_TRUE                 { $$ = new AnnotationArgument(*$name,true); delete $name; }
    |   NAME[name] '=' DAS_FALSE                { $$ = new AnnotationArgument(*$name,false); delete $name; }
    |   NAME[name]                              { $$ = new AnnotationArgument(*$name,true); delete $name; }
    |   NAME[name] '=' '(' annotation_argument_value_list[values] ')' {
        { $$ = new AnnotationArgument(*$name,$values); delete $name; }
    }
    ;

annotation_argument_list
    :   annotation_argument[arg]  {
        $$ = new AnnotationArgumentList();
        if ( $arg->type==Type::none ) {
            for ( auto & sarg : *($arg->aList) ) {
                sarg.name = $arg->name;
                $$->push_back(sarg);
            }
            delete $arg->aList;
        } else {
            $$->push_back(*$arg);
        }
        delete $arg;
    }
    |   annotation_argument_list[argL] ',' annotation_argument[arg] {
            $$ = $argL;
            if ( $arg->type==Type::none ) {
                for ( auto & sarg : *($arg->aList) ) {
                    sarg.name = $arg->name;
                    $$->push_back(sarg);
                }
                delete $arg->aList;
            } else {
                $$->push_back(*$arg);
            }
            delete $arg;
    }
    ;

annotation_declaration_name
    :   name_in_namespace[name]     { $$ = $name; }
    |   DAS_REQUIRE                 { $$ = new string("require"); }
    |   DAS_PRIVATE                 { $$ = new string("private"); }
    ;

annotation_declaration
    :   annotation_declaration_name[name] {
        $$ = new AnnotationDeclaration();
        if ( auto ann = findAnnotation(*$name,tokAt(@name)) ) {
            $$->annotation = ann;
        }
        delete $name;
    }
    |   annotation_declaration_name[name] '(' annotation_argument_list[argL] ')' {
        $$ = new AnnotationDeclaration();
        if ( auto ann = findAnnotation(*$name,tokAt(@name)) ) {
            $$->annotation = ann;
        }
        swap ( $$->arguments, *$argL );
        delete $argL;
        delete $name;
    }
    ;

annotation_list
    :   annotation_declaration[ann] {
            $$ = new AnnotationList();
            $$->push_back(AnnotationDeclarationPtr($ann));
    }
    |   annotation_list[annL] ',' annotation_declaration[ann] {
        $$ = $annL;
        $$->push_back(AnnotationDeclarationPtr($ann));
    }
    ;

optional_annotation_list
    :                                   { $$ = nullptr; }
    |   '[' annotation_list[annL] ']'   { $$ = $annL; }
    ;

optional_function_argument_list
    :                                           { $$ = nullptr; }
    |   '(' ')'                                 { $$ = nullptr; }
    |   '(' function_argument_list[list] ')'    { $$ = $list; }
    ;

optional_function_type
    :   {
        $$ = new TypeDecl(Type::autoinfer);
        $$->at = LineInfo(g_FileAccessStack.back(),
            yylloc.first_column,yylloc.first_line,yylloc.last_column,yylloc.last_line);
    }
    |   ':' type_declaration[typeDecl]  {
        $$ = $typeDecl;
        $$->at = tokAt(@typeDecl);
    }
    ;

function_name
    :   NAME[name]        {
        das_checkName(*$name,tokAt(@name));
        $$ = $name;
    }
    |   DAS_OPERATOR '!'     { $$ = new string("!"); }
    |   DAS_OPERATOR '~'     { $$ = new string("~"); }
    |   DAS_OPERATOR ADDEQU  { $$ = new string("+="); }
    |   DAS_OPERATOR SUBEQU  { $$ = new string("-="); }
    |   DAS_OPERATOR MULEQU  { $$ = new string("*="); }
    |   DAS_OPERATOR DIVEQU  { $$ = new string("/="); }
    |   DAS_OPERATOR MODEQU  { $$ = new string("%="); }
    |   DAS_OPERATOR ANDEQU  { $$ = new string("&="); }
    |   DAS_OPERATOR OREQU   { $$ = new string("|="); }
    |   DAS_OPERATOR XOREQU  { $$ = new string("^="); }
    |   DAS_OPERATOR ANDANDEQU  { $$ = new string("&&="); }
    |   DAS_OPERATOR OROREQU    { $$ = new string("||="); }
    |   DAS_OPERATOR XORXOREQU  { $$ = new string("^^="); }
    |   DAS_OPERATOR ANDAND  { $$ = new string("&&"); }
    |   DAS_OPERATOR OROR    { $$ = new string("||"); }
    |   DAS_OPERATOR XORXOR  { $$ = new string("^^"); }
    |   DAS_OPERATOR '+'     { $$ = new string("+"); }
    |   DAS_OPERATOR '-'     { $$ = new string("-"); }
    |   DAS_OPERATOR '*'     { $$ = new string("*"); }
    |   DAS_OPERATOR '/'     { $$ = new string("/"); }
    |   DAS_OPERATOR '%'     { $$ = new string("%"); }
    |   DAS_OPERATOR '<'     { $$ = new string("<"); }
    |   DAS_OPERATOR '>'     { $$ = new string(">"); }
    |   DAS_OPERATOR EQUEQU  { $$ = new string("=="); }
    |   DAS_OPERATOR NOTEQU  { $$ = new string("!="); }
    |   DAS_OPERATOR LEEQU   { $$ = new string("<="); }
    |   DAS_OPERATOR GREQU   { $$ = new string(">="); }
    |   DAS_OPERATOR '&'     { $$ = new string("&"); }
    |   DAS_OPERATOR '|'     { $$ = new string("|"); }
    |   DAS_OPERATOR '^'     { $$ = new string("^"); }
    |   ADDADD DAS_OPERATOR  { $$ = new string("++"); }
    |   SUBSUB DAS_OPERATOR  { $$ = new string("--"); }
    |   DAS_OPERATOR ADDADD  { $$ = new string("+++"); }
    |   DAS_OPERATOR SUBSUB  { $$ = new string("---"); }
    |   DAS_OPERATOR SHL     { $$ = new string("<<"); }
    |   DAS_OPERATOR SHR     { $$ = new string(">>"); }
    |   DAS_OPERATOR SHLEQU  { $$ = new string("<<="); }
    |   DAS_OPERATOR SHREQU  { $$ = new string(">>="); }
    |   DAS_OPERATOR ROTL    { $$ = new string("<<<"); }
    |   DAS_OPERATOR ROTR    { $$ = new string(">>>"); }
    |   DAS_OPERATOR ROTLEQU { $$ = new string("<<<="); }
    |   DAS_OPERATOR ROTREQU { $$ = new string(">>>="); }
    ;

global_function_declaration
    :   optional_annotation_list[annL] DAS_DEF[from] function_declaration[func] {
        $func->atDecl = tokRangeAt(@from,@func);
        assignDefaultArguments($func);
        runFunctionAnnotations($func, $annL, tokAt(@annL));
        if ( $func->isGeneric() ) {
            if ( !g_Program->addGeneric($func) ) {
                das_yyerror("generic function is already defined " +
                    $func->getMangledName(),$func->at,
                        CompilationError::function_already_declared);
            }
        } else {
            if ( !g_Program->addFunction($func) ) {
                das_yyerror("function is already defined " +
                    $func->getMangledName(),$func->at,
                        CompilationError::function_already_declared);
            }
        }
        $func->delRef();
    }
    ;

function_declaration_header
    :  function_name[name] optional_function_argument_list[list] optional_function_type[result] {
        auto pFunction = make_smart<Function>();
        pFunction->at = tokAt(@name);
        pFunction->name = *$name;
        pFunction->result = TypeDeclPtr($result);
        if ( $list ) {
            for ( auto pDecl : *$list ) {
                if ( pDecl->pTypeDecl ) {
                    for ( const auto & name_at : *pDecl->pNameList ) {
                        if ( !pFunction->findArgument(name_at.first) ) {
                            VariablePtr pVar = make_smart<Variable>();
                            pVar->name = name_at.first;
                            pVar->at = name_at.second;
                            pVar->type = make_smart<TypeDecl>(*pDecl->pTypeDecl);
                            if ( pDecl->pInit ) {
                                pVar->init = ExpressionPtr(pDecl->pInit->clone());
                                pVar->init_via_move = pDecl->init_via_move;
                                pVar->init_via_clone = pDecl->init_via_clone;
                            }
                            if ( pDecl->annotation ) {
                                pVar->annotation = *pDecl->annotation;
                            }
                            pFunction->arguments.push_back(pVar);
                        } else {
                            das_yyerror("function argument is already declared " + name_at.first,name_at.second,
                                CompilationError::argument_already_declared);
                        }
                    }
                }
            }
            deleteVariableDeclarationList($list);
        }
        delete $name;
        $$ = pFunction.orphan();
    }
    ;

function_declaration
    :  function_declaration_header[func] expression_block[block] {
        $func->body = ExpressionPtr($block);
        $$ = $func;
    }
    ;

expression_block
    :   '{' [bbegin] expressions[block] '}' [bend] {
        $$ = $block;
        $$->at = tokRangeAt(@bbegin,@bend);
    }
    |   '{' [bbegin] expressions[block] '}' DAS_FINALLY '{' expressions[final] '}' [bend] {
        auto pB = (ExprBlock *) $block;
        auto pF = (ExprBlock *) $final;
        swap ( pB->finalList, pF->list );
        $$ = $block;
        $$->at = tokRangeAt(@bbegin,@bend);
        delete $final;
    }
    ;

expression_any
    :   ';'                                 { $$ = nullptr; }
    |   expr_pipe[subexpr]                  { $$ = $subexpr; }
    |   expr_assign_pipe[subexpr]           { $$ = $subexpr; }
    |   expr_assign[subexpr] ';'            { $$ = $subexpr; }
    |   expression_delete[subexpr] ';'      { $$ = $subexpr; }
    |   expression_let[subexpr]             { $$ = $subexpr; }
    |   expression_while_loop[subexpr]      { $$ = $subexpr; }
    |   expression_unsafe[subexpr]          { $$ = $subexpr; }
    |   expression_with[subexpr]            { $$ = $subexpr; }
    |   expression_for_loop[subexpr]        { $$ = $subexpr; }
    |   expression_break[subexpr] ';'       { $$ = $subexpr; }
    |   expression_continue[subexpr] ';'    { $$ = $subexpr; }
    |   expression_return[subexpr]          { $$ = $subexpr; }
    |   expression_yield[subexpr]           { $$ = $subexpr; }
    |   expression_if_then_else[subexpr]    { $$ = $subexpr; }
    |   expression_try_catch[subexpr]       { $$ = $subexpr; }
    |   expression_label[subexpr] ';'       { $$ = $subexpr; }
    |   expression_goto[subexpr] ';'        { $$ = $subexpr; }
    |   DAS_PASS ';'                        { $$ = nullptr; }
    ;

expressions
    :   {
        $$ = new ExprBlock();
        $$->at = LineInfo(g_FileAccessStack.back(),
            yylloc.first_column,yylloc.first_line,yylloc.last_column,yylloc.last_line);
    }
    |   expressions[block] expression_any[subexpr]      {
        $$ = $block;
        if ( $subexpr ) {
            static_cast<ExprBlock*>($block)->list.push_back(ExpressionPtr($subexpr));
        }
    }
    |   expressions[block] error {
        delete $block; $$ = nullptr; YYABORT;
    }
    ;

expr_pipe
    :   expr_assign[fncall] LBPIPE[loc] expr_block[arg] {
        Expression * pipeCall = $fncall->tail();
        if ( pipeCall->rtti_isCallLikeExpr() ) {
            auto pCall = (ExprLooksLikeCall *) pipeCall;
            pCall->arguments.push_back(ExpressionPtr($arg));
            $$ = $fncall;
        } else if ( pipeCall->rtti_isVar() ) {
            // a += b <| c
            auto pVar = (ExprVar *) pipeCall;
            auto pCall = g_Program->makeCall(pVar->at,pVar->name);
            pCall->arguments.push_back(ExpressionPtr($arg));
            if ( !$fncall->swap_tail(pVar,pCall) ) {
                delete pVar;
                $$ = pCall;
            } else {
                $$ = $fncall;
            }
        } else if ( pipeCall->rtti_isMakeStruct() ) {
            auto pMS = (ExprMakeStruct *) pipeCall;
            if ( pMS->block ) {
                das_yyerror("can't pipe into [[ make structure ]]. it already has where closure",
                    tokAt(@loc),CompilationError::cant_pipe);
                delete $arg;
            } else {
                pMS->block = $arg;
            }
            $$ = $fncall;
        } else {
            das_yyerror("can only pipe into function call or [[ make structure ]]",
                tokAt(@loc),CompilationError::cant_pipe);
            delete $arg;
            $$ = $fncall;
        }
    }
    |   LAPIPE expr_block[arg] {
        $$ = $arg;
    }
    |   LFPIPE expr_block[arg] {
        $$ = $arg;
    }
    ;

name_in_namespace
    :   NAME[name]                             { $$ = $name; }
    |   NAME[namespace] COLCOL NAME[name]      {
            auto ita = das_module_alias.find(*$namespace);
            if ( ita == das_module_alias.end() ) {
                *$namespace += "::";
            } else {
                *$namespace = ita->second + "::";
            }
            *$namespace += *$name;
            delete $name;
            $$ = $namespace;
        }
    |   COLCOL NAME[name]                      { *$name = "::" + *$name; $$ = $name; }
    ;

expression_delete
    :   DAS_DELETE[loc] expr[subexpr] {
        $$ = new ExprDelete(tokAt(@loc), ExpressionPtr($subexpr));
    }
    ;

expr_new
    :   DAS_NEWT[loc] type_declaration[typeDecl]   {
        $$ = new ExprNew(tokAt(@loc),TypeDeclPtr($typeDecl),false);
    }
    |   DAS_NEWT[loc] type_declaration[typeDecl] '(' ')'   {
        $$ = new ExprNew(tokAt(@loc),TypeDeclPtr($typeDecl),true);
    }
    |   DAS_NEWT[loc] type_declaration[typeDecl] '(' expr_list[arguments] ')'   {
        auto pNew = new ExprNew(tokAt(@loc),TypeDeclPtr($typeDecl),true);
        $$ = parseFunctionArguments(pNew,$arguments);
    }
    |   DAS_NEWT[loc] make_decl[md] {
        $$ = new ExprAscend(tokAt(@loc),ExpressionPtr($md));
    }
    ;

expression_break
    :   DAS_BREAK[loc] { $$ = new ExprBreak(tokAt(@loc)); }
    ;

expression_continue
    :   DAS_CONTINUE[loc] { $$ = new ExprContinue(tokAt(@loc)); }
    ;

expression_return
    :   DAS_RETURN[loc] ';' {
        $$ = new ExprReturn(tokAt(@loc),nullptr);
    }
    |   DAS_RETURN[loc] expr[subexpr] ';' {
        $$ = new ExprReturn(tokAt(@loc),ExpressionPtr($subexpr));
    }
    |   DAS_RETURN[loc] LARROW expr[subexpr] ';' {
        auto pRet = new ExprReturn(tokAt(@loc),ExpressionPtr($subexpr));
        pRet->moveSemantics = true;
        $$ = pRet;
    }
    |   DAS_RETURN[loc] expr_pipe[subexpr] {
        $$ = new ExprReturn(tokAt(@loc),ExpressionPtr($subexpr));
    }
    |   DAS_RETURN[loc] LARROW expr_pipe[subexpr] {
        auto pRet = new ExprReturn(tokAt(@loc),ExpressionPtr($subexpr));
        pRet->moveSemantics = true;
        $$ = pRet;
    }
    ;

expression_yield
    :   DAS_YIELD[loc] expr[subexpr] ';' {
        $$ = new ExprYield(tokAt(@loc),ExpressionPtr($subexpr));
    }
    |   DAS_YIELD[loc] LARROW expr[subexpr] ';' {
        auto pRet = new ExprYield(tokAt(@loc),ExpressionPtr($subexpr));
        pRet->moveSemantics = true;
        $$ = pRet;
    }
    |   DAS_YIELD[loc] expr_pipe[subexpr] {
        $$ = new ExprYield(tokAt(@loc),ExpressionPtr($subexpr));
    }
    |   DAS_YIELD[loc] LARROW expr_pipe[subexpr] {
        auto pRet = new ExprYield(tokAt(@loc),ExpressionPtr($subexpr));
        pRet->moveSemantics = true;
        $$ = pRet;
    }
    ;

expression_try_catch
    :   DAS_TRY[loc] expression_block[tryBlock] DAS_CATCH expression_block[catchBlock] {
        $$ = new ExprTryCatch(tokAt(@loc),ExpressionPtr($tryBlock),ExpressionPtr($catchBlock));
    }
    ;

kwd_let
    :   DAS_LET  { $$ = true; }
    |   DAS_VAR  { $$ = false; }
    ;

expression_let
    :   kwd_let let_variable_declaration[decl] {
        auto pLet = new ExprLet();
        pLet->at = tokAt(@kwd_let);
        pLet->atInit = tokAt(@decl);
        if ( $decl->pTypeDecl ) {
            for ( const auto & name_at : *$decl->pNameList ) {
                if ( !pLet->find(name_at.first) ) {
                    VariablePtr pVar = make_smart<Variable>();
                    pVar->name = name_at.first;
                    pVar->at = name_at.second;
                    pVar->type = make_smart<TypeDecl>(*$decl->pTypeDecl);
                    if ( $decl->pInit ) {
                        pVar->init = $decl->pInit->clone();
                        pVar->init_via_move = $decl->init_via_move;
                        pVar->init_via_clone = $decl->init_via_clone;
                    }
                    if ( $kwd_let ) {
                        pVar->type->constant = true;
                    } else {
                        pVar->type->removeConstant = true;
                    }
                    pLet->variables.push_back(pVar);
                } else {
                    das_yyerror("local variable is already declared " + name_at.first,name_at.second,
                        CompilationError::local_variable_already_declared);
                }
            }
        }
        delete $decl;
        $$ = pLet;
    }
    ;

expr_cast
    :   DAS_CAST[loc] '<' { das_arrow_depth ++; } type_declaration[decl] '>' { das_arrow_depth --; } expr[subexpr] {
        $$ = new ExprCast(tokAt(@loc),ExpressionPtr($subexpr),TypeDeclPtr($decl));
    }
    |   DAS_UPCAST[loc] '<' { das_arrow_depth ++; } type_declaration[decl] '>' { das_arrow_depth --; } expr[subexpr] {
        auto pCast = new ExprCast(tokAt(@loc),ExpressionPtr($subexpr),TypeDeclPtr($decl));
        pCast->upcast = true;
        $$ = pCast;
    }
    |   DAS_REINTERPRET[loc] '<' { das_arrow_depth ++; } type_declaration[decl] '>' { das_arrow_depth --; } expr[subexpr] {
        auto pCast = new ExprCast(tokAt(@loc),ExpressionPtr($subexpr),TypeDeclPtr($decl));
        pCast->reinterpret = true;
        $$ = pCast;
    }
    ;

expr_type_decl
    :  DAS_TYPE[loc] '<' { das_arrow_depth ++; } type_declaration[decl] '>' { das_arrow_depth --; } {
        $$ = new ExprTypeDecl(tokAt(@loc),TypeDeclPtr($decl));
    }
    ;

expr_type_info
    :   DAS_TYPEINFO[loc] '(' name_in_namespace[trait] expr[subexpr] ')' {
            if ( $subexpr->rtti_isTypeDecl() ) {
                auto ptd = (ExprTypeDecl *)$subexpr;
                $$ = new ExprTypeInfo(tokAt(@loc),*$trait,ptd->typeexpr);
                delete $subexpr;
            } else {
                $$ = new ExprTypeInfo(tokAt(@loc),*$trait,ExpressionPtr($subexpr));
            }
            delete $trait;
    }
    |   DAS_TYPEINFO[loc] '(' name_in_namespace[trait] '<' NAME[subtrait] '>' expr[subexpr] ')' {
            if ( $subexpr->rtti_isTypeDecl() ) {
                auto ptd = (ExprTypeDecl *)$subexpr;
                $$ = new ExprTypeInfo(tokAt(@loc),*$trait,ptd->typeexpr,*$subtrait);
                delete $subexpr;
            } else {
                $$ = new ExprTypeInfo(tokAt(@loc),*$trait,ExpressionPtr($subexpr),*$subtrait);
            }
            delete $trait;
            delete $subtrait;
    }
    |   DAS_TYPEINFO[loc] '(' name_in_namespace[trait] '<' NAME[subtrait] ';' NAME[extratrait] '>' expr[subexpr] ')' {
            if ( $subexpr->rtti_isTypeDecl() ) {
                auto ptd = (ExprTypeDecl *)$subexpr;
                $$ = new ExprTypeInfo(tokAt(@loc),*$trait,ptd->typeexpr,*$subtrait,*$extratrait);
                delete $subexpr;
            } else {
                $$ = new ExprTypeInfo(tokAt(@loc),*$trait,ExpressionPtr($subexpr),*$subtrait,*$extratrait);
            }
            delete $trait;
            delete $subtrait;
            delete $extratrait;
    }
    ;

expr_list
    :   expr[subexpr] {
        $$ = $subexpr;
    }
    |   expr_list[left] ',' expr[right]     {
            $$ = new ExprSequence(tokAt(@left),ExpressionPtr($left),ExpressionPtr($right));
    }
    ;

block_or_simple_block
    :   expression_block[block]     { $$ = $block; }
    |   MAPTO[loc] expr[subexpr]        {
            auto retE = make_smart<ExprReturn>(tokAt(@loc), ExpressionPtr($subexpr));
            auto blkE = new ExprBlock();
            blkE->at = tokAt(@loc);
            blkE->list.push_back(retE);
            $$ = blkE;
    }
    |   MAPTO[loc] LARROW expr[subexpr]        {
            auto retE = make_smart<ExprReturn>(tokAt(@loc), ExpressionPtr($subexpr));
            retE->moveSemantics = true;
            auto blkE = new ExprBlock();
            blkE->at = tokAt(@loc);
            blkE->list.push_back(retE);
            $$ = blkE;
    }
    ;

block_or_lambda
    :   '$'     { $$ = 0;   /* block */  }
    |   '@'     { $$ = 1;   /* lambda */ }
    |   '@' '@' { $$ = 2;   /* local function */ }
    ;

capture_entry
    :   '&' NAME[name]         { $$ = new CaptureEntry(*$name,CaptureMode::capture_by_reference); delete $name; }
    |   '=' NAME[name]         { $$ = new CaptureEntry(*$name,CaptureMode::capture_by_copy); delete $name; }
    |   LARROW NAME[name]      { $$ = new CaptureEntry(*$name,CaptureMode::capture_by_move); delete $name; }
    |   CLONEEQU NAME[name]    { $$ = new CaptureEntry(*$name,CaptureMode::capture_by_clone); delete $name; }
    ;

capture_list
    :  capture_entry[ce] {
        $$ = new vector<CaptureEntry>();
        $$->push_back(*$ce);
        delete $ce;
    }
    |   capture_list[cl] ',' capture_entry[ce] {
        $cl->push_back(*$ce);
        delete $ce;
        $$ = $cl;
    }
    ;

optional_capture_list
    :   { $$ = nullptr; }
    |   BRABRAB capture_list[cl] ']' ']' { $$ = $cl; }
    ;

expr_block
    :   expression_block[block]             {
        ExprBlock * closure = (ExprBlock *) $block;
        $$ = new ExprMakeBlock(tokAt(@block),ExpressionPtr($block));
        closure->returnType = make_smart<TypeDecl>(Type::autoinfer);
    }
    |   block_or_lambda[bal] optional_annotation_list[annL] optional_capture_list[clist] optional_function_argument_list[list]
                                optional_function_type[result] block_or_simple_block[block] {
        auto mkb = new ExprMakeBlock(tokAt(@block),ExpressionPtr($block), $bal==1, $bal==2);
        $$ = mkb;
        ExprBlock * closure = (ExprBlock *) $block;
        closure->returnType = TypeDeclPtr($result);
        if ( $list ) {
            for ( auto pDecl : *$list ) {
                if ( pDecl->pTypeDecl ) {
                    for ( const auto & name_at : *pDecl->pNameList ) {
                        if ( !closure->findArgument(name_at.first) ) {
                            VariablePtr pVar = make_smart<Variable>();
                            pVar->name = name_at.first;
                            pVar->at = name_at.second;
                            pVar->type = make_smart<TypeDecl>(*pDecl->pTypeDecl);
                            if ( pDecl->pInit ) {
                                pVar->init = ExpressionPtr(pDecl->pInit->clone());
                                pVar->init_via_move = pDecl->init_via_move;
                                pVar->init_via_clone = pDecl->init_via_clone;
                            }
                            if ( pDecl->annotation ) {
                                pVar->annotation = *pDecl->annotation;
                            }
                            closure->arguments.push_back(pVar);
                        } else {
                            das_yyerror("block argument is already declared " + name_at.first,
                                name_at.second,CompilationError::argument_already_declared);
                        }
                    }
                }
            }
            deleteVariableDeclarationList($list);
        }
        if ( $clist ) {
            swap ( mkb->capture, *$clist );
            delete $clist;
            if ( $bal != 1 ) {   // if its not lambda, can't capture
                das_yyerror("can only have capture section for lambda",
                    mkb->at,CompilationError::invalid_capture);
            }
        }
        if ( $annL ) {
            for ( auto pA : *$annL ) {
                if ( pA->annotation ) {
                    if ( pA->annotation->rtti_isFunctionAnnotation() ) {
                        auto ann = static_pointer_cast<FunctionAnnotation>(pA->annotation);
                        string err;
                        if ( !ann->apply(closure, *g_Program->thisModuleGroup, pA->arguments, err) ) {
                            das_yyerror("can't apply annotation\n" + err, tokAt(@annL),
                                CompilationError::invalid_annotation);
                        }
                    } else {
                        das_yyerror("blocks are only allowed function annotations", tokAt(@annL),
                            CompilationError::invalid_annotation);
                    }
                }
            }
            swap ( closure->annotations, *$annL );
            delete $annL;
        }
    }
    ;

expr_numeric_const
    :   INTEGER[const]                        { $$ = new ExprConstInt(tokAt(@const),(int32_t)$const); }
    |   UNSIGNED_INTEGER[const]               { $$ = new ExprConstUInt(tokAt(@const),(uint32_t)$const); }
    |   LONG_INTEGER[const]                   { $$ = new ExprConstInt64(tokAt(@const),(int64_t)$const); }
    |   UNSIGNED_LONG_INTEGER[const]          { $$ = new ExprConstUInt64(tokAt(@const),(uint64_t)$const); }
    |   FLOAT[const]                          { $$ = new ExprConstFloat(tokAt(@const),(float)$const); }
    |   DOUBLE[const]                         { $$ = new ExprConstDouble(tokAt(@const),(double)$const); }
    ;

expr_assign
    :   expr[ex]                             { $$ = $ex; }
    |   expr[left] '='[loc]    expr[right]   { $$ = new ExprCopy(tokAt(@loc),ExpressionPtr($left),ExpressionPtr($right)); }
    |   expr[left] LARROW[loc] expr[right]   { $$ = new ExprMove(tokAt(@loc),ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] CLONEEQU[loc] expr[right] { $$ = new ExprClone(tokAt(@loc),ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] ANDEQU[loc] expr[right]   { $$ = new ExprOp2(tokAt(@loc),"&=", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] OREQU[loc] expr[right]    { $$ = new ExprOp2(tokAt(@loc),"|=", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] XOREQU[loc] expr[right]   { $$ = new ExprOp2(tokAt(@loc),"^=", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] ANDANDEQU[loc] expr[right]   { $$ = new ExprOp2(tokAt(@loc),"&&=", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] OROREQU[loc] expr[right]     { $$ = new ExprOp2(tokAt(@loc),"||=", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] XORXOREQU[loc] expr[right]   { $$ = new ExprOp2(tokAt(@loc),"^^=", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] ADDEQU[loc] expr[right]   { $$ = new ExprOp2(tokAt(@loc),"+=", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] SUBEQU[loc] expr[right]   { $$ = new ExprOp2(tokAt(@loc),"-=", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] MULEQU[loc] expr[right]   { $$ = new ExprOp2(tokAt(@loc),"*=", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] DIVEQU[loc] expr[right]   { $$ = new ExprOp2(tokAt(@loc),"/=", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] MODEQU[loc] expr[right]   { $$ = new ExprOp2(tokAt(@loc),"%=", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] SHLEQU[loc] expr[right]   { $$ = new ExprOp2(tokAt(@loc),"<<=", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] SHREQU[loc] expr[right]   { $$ = new ExprOp2(tokAt(@loc),">>=", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] ROTLEQU[loc] expr[right]  { $$ = new ExprOp2(tokAt(@loc),"<<<=", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] ROTREQU[loc] expr[right]  { $$ = new ExprOp2(tokAt(@loc),">>>=", ExpressionPtr($left), ExpressionPtr($right)); }
    ;

expr_assign_pipe
    :   expr[left] '='[loc]    LAPIPE expr[right]   { $$ = new ExprCopy(tokAt(@loc),ExpressionPtr($left),ExpressionPtr($right)); }
    |   expr[left] '='[loc]    LFPIPE expr[right]   { $$ = new ExprCopy(tokAt(@loc),ExpressionPtr($left),ExpressionPtr($right)); }
    |   expr[left] LARROW[loc] LAPIPE expr[right]   { $$ = new ExprMove(tokAt(@loc),ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] LARROW[loc] LFPIPE expr[right]   { $$ = new ExprMove(tokAt(@loc),ExpressionPtr($left), ExpressionPtr($right)); }
    ;

expr_named_call
    :   name_in_namespace[name] '(' '[' make_struct_fields[list] ']' ')' {
        auto nc = new ExprNamedCall(tokAt(@name),*$name);
        nc->arguments = *$list;
        delete $list;
        delete $name;
        $$ = nc;
    }
    ;

/* a->b(args) is short for invoke(a.b, a, args)  */
expr_method_call
    :   expr[left] RARROW[loc] NAME[method_name] '(' ')' {
        auto pInvoke = makeInvokeMethod(tokAt(@loc), $left, *$method_name);
        delete $method_name;
        $$ = pInvoke;
    }
    |   expr[left] RARROW[loc] NAME[method_name] '(' expr_list[arguments] ')' {
        auto pInvoke = makeInvokeMethod(tokAt(@loc), $left, *$method_name);
        auto callArgs = sequenceToList($arguments);
        pInvoke->arguments.insert ( pInvoke->arguments.end(), callArgs.begin(), callArgs.end() );
        delete $method_name;
        $$ = pInvoke;
    }
    ;

func_addr_expr
    :   '@' '@'[loc] name_in_namespace[name]     {
        $$ = new ExprAddr(tokAt(@loc),*$name);
        delete $name;
    }
    |   '@' '@'[loc] '<' { das_arrow_depth ++; } type_declaration[blockType] '>' { das_arrow_depth --; } name_in_namespace[name] {
        auto expr = new ExprAddr(tokAt(@loc),*$name);
        delete $name;
        expr->funcType = make_smart<TypeDecl>(Type::tFunction);
        expr->funcType->firstType = TypeDeclPtr($blockType);
        $$ = expr;
    }
    |   '@' '@'[loc] '<' { das_arrow_depth ++; } optional_function_argument_list[list] optional_function_type[result] '>' { das_arrow_depth --; } name_in_namespace[name] {
        auto expr = new ExprAddr(tokAt(@loc),*$name);
        delete $name;
        expr->funcType = make_smart<TypeDecl>(Type::tFunction);
        expr->funcType->firstType = TypeDeclPtr($result);
        if ( $list ) {
            varDeclToTypeDecl(expr->funcType.get(), $list);
            deleteVariableDeclarationList($list);
        }
        $$ = expr;
    }
    ;

expr_field
    :   expr[subexpr] '.'[loc] NAME[name]     {
        $$ = new ExprField(tokAt(@loc), tokAt(@name), ExpressionPtr($subexpr), *$name);
        delete $name;
    }
    |   expr[subexpr] '.'[loc] { das_supress_errors=true; } error { das_supress_errors=false; } {
        $$ = new ExprField(tokAt(@loc), tokAt(@loc), ExpressionPtr($subexpr), "");
        yyerrok;
    }
    ;

expr
    :   DAS_NULL[loc]                         { $$ = new ExprConstPtr(tokAt(@loc),nullptr); }
    |   name_in_namespace[name]               { $$ = new ExprVar(tokAt(@name),*$name); delete $name; }
    |   expr_numeric_const[nc]                { $$ = $nc; }
    |   expr_reader[rdr]                      { $$ = $rdr; }
    |   string_builder[sb]                    { $$ = $sb; }
    |   make_decl[md]                         { $$ = $md; }
    |   DAS_TRUE[loc]                         { $$ = new ExprConstBool(tokAt(@loc),true); }
    |   DAS_FALSE[loc]                        { $$ = new ExprConstBool(tokAt(@loc),false); }
    |   expr_field[subexpr]                   { $$ = $subexpr; }
    |   '!'[loc] expr[subexpr]                { $$ = new ExprOp1(tokAt(@loc),"!",ExpressionPtr($subexpr)); }
    |   '~'[loc] expr[subexpr]                { $$ = new ExprOp1(tokAt(@loc),"~",ExpressionPtr($subexpr)); }
    |   '+'[loc] expr[subexpr] %prec  UNARY_PLUS  { $$ = new ExprOp1(tokAt(@loc),"+",ExpressionPtr($subexpr)); }
    |   '-'[loc] expr[subexpr] %prec  UNARY_MINUS { $$ = new ExprOp1(tokAt(@loc),"-",ExpressionPtr($subexpr)); }
    |   expr[left] SHL[loc]    expr[right]   { $$ = new ExprOp2(tokAt(@loc),"<<", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] SHR[loc]    expr[right]   { $$ = new ExprOp2(tokAt(@loc),">>", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] ROTL[loc]    expr[right]  { $$ = new ExprOp2(tokAt(@loc),"<<<", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] ROTR[loc]    expr[right]  { $$ = new ExprOp2(tokAt(@loc),">>>", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] '+'[loc]    expr[right]   { $$ = new ExprOp2(tokAt(@loc),"+", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] '-'[loc]    expr[right]   { $$ = new ExprOp2(tokAt(@loc),"-", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] '*'[loc]    expr[right]   { $$ = new ExprOp2(tokAt(@loc),"*", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] '/'[loc]    expr[right]   { $$ = new ExprOp2(tokAt(@loc),"/", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] '%'[loc]    expr[right]   { $$ = new ExprOp2(tokAt(@loc),"%", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] '<'[loc]    expr[right]   { $$ = new ExprOp2(tokAt(@loc),"<", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] '>'[loc]    expr[right]   { $$ = new ExprOp2(tokAt(@loc),">", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] EQUEQU[loc] expr[right]   { $$ = new ExprOp2(tokAt(@loc),"==", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] NOTEQU[loc] expr[right]   { $$ = new ExprOp2(tokAt(@loc),"!=", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] LEEQU[loc]  expr[right]   { $$ = new ExprOp2(tokAt(@loc),"<=", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] GREQU[loc]  expr[right]   { $$ = new ExprOp2(tokAt(@loc),">=", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] '&'[loc] expr[right]      { $$ = new ExprOp2(tokAt(@loc),"&", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] '|'[loc] expr[right]      { $$ = new ExprOp2(tokAt(@loc),"|", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] '^'[loc] expr[right]      { $$ = new ExprOp2(tokAt(@loc),"^", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] ANDAND[loc] expr[right]   { $$ = new ExprOp2(tokAt(@loc),"&&", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] OROR[loc] expr[right]     { $$ = new ExprOp2(tokAt(@loc),"||", ExpressionPtr($left), ExpressionPtr($right)); }
    |   expr[left] XORXOR[loc] expr[right]   { $$ = new ExprOp2(tokAt(@loc),"^^", ExpressionPtr($left), ExpressionPtr($right)); }
    |   ADDADD[loc] expr[subexpr] %prec PRE_INC  { $$ = new ExprOp1(tokAt(@loc),"++", ExpressionPtr($subexpr)); }
    |   SUBSUB[loc] expr[subexpr] %prec PRE_DEC  { $$ = new ExprOp1(tokAt(@loc),"--", ExpressionPtr($subexpr)); }
    |   expr[subexpr] ADDADD[loc] %prec POST_INC { $$ = new ExprOp1(tokAt(@loc),"+++", ExpressionPtr($subexpr)); }
    |   expr[subexpr] SUBSUB[loc] %prec POST_DEC { $$ = new ExprOp1(tokAt(@loc),"---", ExpressionPtr($subexpr)); }
    |   '(' expr[subexpr] ')'                    { $$ = $subexpr; }
    |   expr[subexpr] '['[loc] expr[index] ']'   { $$ = new ExprAt(tokAt(@loc), ExpressionPtr($subexpr), ExpressionPtr($index)); }
    |   expr[subexpr] QBRA[loc] expr[index] ']'  { $$ = new ExprSafeAt(tokAt(@loc), ExpressionPtr($subexpr), ExpressionPtr($index)); }
    |   expr[subexpr] QDOT[loc] NAME[name]       { $$ = new ExprSafeField(tokAt(@loc), tokAt(@name), ExpressionPtr($subexpr), *$name); delete $name; }
    |   func_addr_expr[subexpr]                  { $$ = $subexpr; }
    |   name_in_namespace[name] '(' ')' {
            $$ = g_Program->makeCall(tokAt(@name),*$name);
            delete $name;
        }
    |   name_in_namespace[name] '(' expr_list[arguments] ')' {
            $$ = parseFunctionArguments(g_Program->makeCall(tokAt(@name),*$name),$arguments);
            delete $name;
        }
    |   basic_type_declaration[type] '(' ')' {
        $$ = g_Program->makeCall(tokAt(@type),das_to_string($type));
    }
    |   basic_type_declaration[type] '(' expr_list[arguments] ')' {
        $$ = parseFunctionArguments(g_Program->makeCall(tokAt(@type),das_to_string($type)),$arguments);
    }
    |   '*'[loc] expr[subexpr] %prec DEREF         { $$ = new ExprPtr2Ref(tokAt(@loc),ExpressionPtr($subexpr)); }
    |   DAS_DEREF[loc] '(' expr[subexpr] ')'       { $$ = new ExprPtr2Ref(tokAt(@loc),ExpressionPtr($subexpr)); }
    |   DAS_ADDR[loc] '(' expr[subexpr] ')'        { $$ = new ExprRef2Ptr(tokAt(@loc),ExpressionPtr($subexpr)); }
    |   DAS_GENERATOR[loc] '<' type_declaration[typeDecl] '>' optional_capture_list[clist] '(' ')' {
        auto gen = new ExprMakeGenerator(tokAt(@loc));
        gen->iterType = TypeDeclPtr($typeDecl);
        if ( $clist ) {
            swap ( gen->capture, *$clist );
            delete $clist;
        }
        $$ = gen;
    }
    |   DAS_GENERATOR[loc] '<' type_declaration[typeDecl] '>' optional_capture_list[clist] '(' expr[subexpr] ')' {
        auto gen = new ExprMakeGenerator(tokAt(@loc), ExpressionPtr($subexpr));
        gen->iterType = TypeDeclPtr($typeDecl);
        if ( $clist ) {
            swap ( gen->capture, *$clist );
            delete $clist;
        }
        $$ = gen;
    }
    |   expr[subexpr] QQ[loc] expr[dval]           { $$ = new ExprNullCoalescing(tokAt(@loc),ExpressionPtr($subexpr),ExpressionPtr($dval)); }
    |   expr[subexpr] '?'[loc] expr[left] ':' expr[right] {
            $$ = new ExprOp3(tokAt(@loc),"?",ExpressionPtr($subexpr),ExpressionPtr($left),ExpressionPtr($right));
        }
    |   expr[subexpr] DAS_IS[loc] DAS_TYPE '<' { das_arrow_depth ++; } type_declaration[decl] '>' { das_arrow_depth --; } {
        $$ = new ExprIs(tokAt(@loc),ExpressionPtr($subexpr),TypeDeclPtr($decl));
    }
    |   expr[subexpr] DAS_IS[loc] NAME[vname] {
        $$ = new ExprIsVariant(tokAt(@loc),ExpressionPtr($subexpr),*$vname);
        delete $vname;
    }
    |   expr[subexpr] DAS_AS[loc] NAME[vname] {
        $$ = new ExprAsVariant(tokAt(@loc),ExpressionPtr($subexpr),*$vname);
        delete $vname;
    }
    |   expr[subexpr] '?' DAS_AS[loc] NAME[vname] {
        $$ = new ExprSafeAsVariant(tokAt(@loc),ExpressionPtr($subexpr),*$vname);
        delete $vname;
    }
    |   expr_type_info[subexpr]                 { $$ = $subexpr; }
    |   expr_type_decl[subexpr]                 { $$ = $subexpr; }
    |   expr_cast[subexpr]                      { $$ = $subexpr; }
    |   expr_new[subexpr]                       { $$ = $subexpr; }
    |   expr_method_call[subexpr]               { $$ = $subexpr; }
    |   expr_named_call[subexpr]                { $$ = $subexpr; }
    |   expr_block[subexpr]                     { $$ = $subexpr; }
    |   expr[fncall] LPIPE[loc] expr[arg] {
        if ( $fncall->rtti_isCallLikeExpr() ) {
            auto pCall = (ExprLooksLikeCall *) $fncall;
            pCall->arguments.push_back(ExpressionPtr($arg));
            $$ = $fncall;
        } else if ( $fncall->rtti_isVar() ) {
            auto pVar = (ExprVar *) $fncall;
            auto pCall = g_Program->makeCall(pVar->at,pVar->name);
            delete pVar;
            pCall->arguments.push_back(ExpressionPtr($arg));
            $$ = pCall;
        } else {
            das_yyerror("can only lpipe into a function call",tokAt(@loc),CompilationError::cant_pipe);
            $$ = $fncall;
        }
    }
    |   expr[arg] RPIPE[loc] expr[fncall] {
        if ( $fncall->rtti_isCallLikeExpr() ) {
            auto pCall = (ExprLooksLikeCall *) $fncall;
            pCall->arguments.insert(pCall->arguments.begin(),ExpressionPtr($arg));
            $$ = $fncall;
        } else if ( $fncall->rtti_isVar() ) {
            auto pVar = (ExprVar *) $fncall;
            auto pCall = g_Program->makeCall(pVar->at,pVar->name);
            delete pVar;
            pCall->arguments.insert(pCall->arguments.begin(),ExpressionPtr($arg));
            $$ = pCall;
        } else {
            das_yyerror("can only lpipe into a function call",tokAt(@loc),CompilationError::cant_pipe);
            $$ = $fncall;
        }
    }
    | name_in_namespace[ena] NAME[eni] {
        Enumeration * pEnum = nullptr;
        Expression * resConst = nullptr;
        auto enums = g_Program->findEnum(*$ena);
        auto aliases = g_Program->findAlias(*$ena);
        if ( enums.size()+aliases.size() > 1 ) {
            string candidates;
            if ( enums.size() ) candidates += g_Program->describeCandidates(enums);
            if ( aliases.size() ) candidates += g_Program->describeCandidates(aliases);
            das_yyerror("too many options for " + *$ena + "\n" + candidates, tokAt(@ena),
                CompilationError::type_not_found);
        } else if ( enums.size()==0 && aliases.size()==0 ) {
            das_yyerror("enumeration or bitfield not found " + *$ena, tokAt(@ena),
                CompilationError::type_not_found);
        } else if ( enums.size()==1 ) {
            pEnum = enums.back().get();
        } else if ( aliases.size()==1 ) {
            auto alias = aliases.back();
            if ( alias->isEnum() ) {
                pEnum = alias->enumType;
            } else if ( alias->isBitfield() ) {
                int bit = alias->findArgumentIndex(*$eni);
                if ( bit!=-1 ) {
                    auto td = make_smart<TypeDecl>(*alias);
                    td->ref = false;
                    auto bitConst = new ExprConstBitfield(tokAt(@eni), 1u << bit);
                    bitConst->bitfieldType = make_smart<TypeDecl>(*alias);
                    resConst = bitConst;
                } else {
                    das_yyerror("enumeration or bitfield not found " + *$ena, tokAt(@ena),
                        CompilationError::bitfield_not_found);
                }
            } else {
                das_yyerror("expecting enumeration or bitfield " + *$ena, tokAt(@ena),
                    CompilationError::syntax_error);
            }
        }
        if ( pEnum ) {
            auto ff = pEnum->find(*$eni);
            if ( ff.second ) {
                auto td = make_smart<TypeDecl>(pEnum);
                resConst = new ExprConstEnumeration(tokAt(@eni), *$eni, td);
            } else {
                das_yyerror("enumeraiton value not found " + *$ena + " " + *$eni, tokAt(@eni),
                    CompilationError::enumeration_not_found);
            }
        }
        if ( resConst ) {
            $$ = resConst;
        } else {
            $$ = new ExprConstInt(0);   // dummy
        }
        delete $ena;
        delete $eni;
    }
    ;

optional_field_annotation
    :                                                   { $$ = nullptr; }
    |   BRABRAB annotation_argument_list[alist] ']' ']' { $$ = $alist; }
    ;

optional_override
    :                   { $$ = false; }
    |   DAS_OVERRIDE    { $$ = true; }
    ;

structure_variable_declaration
    :   optional_field_annotation[alist] optional_override[ovr] variable_declaration[decl] {
        $decl->override = $ovr;
        $decl->annotation = $alist;
        $$ = $decl;
    }
    ;

struct_variable_declaration_list
    :   {
        $$ = new vector<VariableDeclaration*>();
    }
    |   struct_variable_declaration_list[list] structure_variable_declaration[decl] ';' {
        $$ = $list;
        if ( $decl ) $list->push_back($decl);
    }
    |   struct_variable_declaration_list[list] optional_annotation_list[annL]
            DAS_DEF[from] DAS_ABSTRACT { das_force_oxford_comma=true; } function_declaration_header[func] ';' {
        if ( !g_thisStructure->isClass ) {
            das_yyerror("structure can't have member function",
                $func->at, CompilationError::invalid_member_function);
        } else if ( $func->isGeneric() ) {
            das_yyerror("generic function can't be a member of a class " + $func->getMangledName(),
                $func->at, CompilationError::invalid_member_function);
        } else if ( $func->name==g_thisStructure->name || $func->name=="finalize" ) {
            das_yyerror("initializers and finalizers can't be abstract " + $func->getMangledName(),
                $func->at, CompilationError::invalid_member_function);
        } else if ( $annL!=nullptr ) {
            das_yyerror("abstract functions can't have annotations " + $func->getMangledName(),
                $func->at, CompilationError::invalid_member_function);
            delete $annL;
        } else if ( $func->result->baseType==Type::autoinfer ) {
            das_yyerror("abstract functions must specify return type explicitly " + $func->getMangledName(),
                $func->at, CompilationError::invalid_member_function);
        } else {
            auto varName = $func->name;
            $func->name = g_thisStructure->name + "`" + $func->name;
            auto vars = new vector<pair<string,LineInfo>>();
            vars->emplace_back(make_pair(varName,$func->at));
            TypeDecl * funcType = new TypeDecl(Type::tFunction);
            funcType->at = $func->at;
            swap ( funcType->firstType, $func->result );
            funcType->argTypes.reserve ( $func->arguments.size() );
            if ( g_thisStructure->isClass ) {
                funcType->argTypes.push_back(make_smart<TypeDecl>(g_thisStructure));
                funcType->argNames.push_back("self");
            }
            for ( auto & arg : $func->arguments ) {
                funcType->argTypes.push_back(arg->type);
                funcType->argNames.push_back(arg->name);
            }
            VariableDeclaration * decl = new VariableDeclaration(
                vars,
                funcType,
                nullptr
            );
            $list->push_back(decl);
        }
        $func->delRef();
        $$ = $list;
    }
    |   struct_variable_declaration_list[list] optional_annotation_list[annL] DAS_DEF[from] optional_override[ovr] function_declaration[func] {
        $func->atDecl = tokRangeAt(@from,@func);
        if ( !g_thisStructure ) {
            das_yyerror("internal error. member function outside of class.",
                $func->at, CompilationError::invalid_member_function);
        } else if ( !g_thisStructure->isClass ) {
            das_yyerror("structure can't have member function",
                $func->at, CompilationError::invalid_member_function);
        } else if ( $func->isGeneric() ) {
            das_yyerror("generic function can't be a member of a class " + $func->getMangledName(),
                $func->at, CompilationError::invalid_member_function);
        } else {
            if ( $func->name != g_thisStructure->name && $func->name != "finalize") {
                auto varName = $func->name;
                $func->name = g_thisStructure->name + "`" + $func->name;
                auto vars = new vector<pair<string,LineInfo>>();
                vars->emplace_back(make_pair(varName,$func->at));
                Expression * finit = new ExprAddr($func->at, inThisModule($func->name));
                if ( $ovr ) {
                    finit = new ExprCast($func->at, finit, make_smart<TypeDecl>(Type::autoinfer));
                }
                VariableDeclaration * decl = new VariableDeclaration(
                    vars,
                    new TypeDecl(Type::autoinfer),
                    finit
                );
                decl->override = $ovr;
                $list->push_back(decl);
                modifyToClassMember($func, g_thisStructure, false);
            } else {
                if ( $ovr ) {
                    das_yyerror("can't override constructor " + $func->getMangledName(),
                        $func->at, CompilationError::invalid_member_function);
                }
                if ( $func->name!="finalize" ) {
                    auto ctr = makeClassConstructor(g_thisStructure, $func);
                    if ( !g_Program->addFunction(ctr) ) {
                        das_yyerror("intializer is already defined " + ctr->getMangledName(),
                            ctr->at, CompilationError::function_already_declared);
                    }
                    $func->name = g_thisStructure->name + "`" + g_thisStructure->name;
                    modifyToClassMember($func, g_thisStructure, false);
                } else {
                    modifyToClassMember($func, g_thisStructure, true);
                }
            }
            assignDefaultArguments($func);
            runFunctionAnnotations($func, $annL, tokAt(@annL));
            if ( !g_Program->addFunction($func) ) {
                das_yyerror("function is already defined " + $func->getMangledName(),
                    $func->at, CompilationError::function_already_declared);
            }
            $func->delRef();
        }
        $$ = $list;
    }
    |   struct_variable_declaration_list[list] '[' annotation_list[annL] ']' ';' {
        das_yyerror("Structure field or class method annotation expected to remain on the same line with field or class.",
            tokAt(@annL), CompilationError::syntax_error);
        delete $annL;
        $$ = $list;
    }
    ;

function_argument_declaration
    :   optional_field_annotation[ann] variable_declaration[decl]          {
            $$ = $decl;
            $decl->pTypeDecl->constant = true;
            $decl->annotation = $ann;
        }
    |   optional_field_annotation[ann] DAS_VAR variable_declaration[decl]  {
            $$ = $decl;
            $decl->pTypeDecl->removeConstant = true;
            $decl->annotation = $ann;
        }
    ;

function_argument_list
    :   function_argument_declaration[decl]                                      { $$ = new vector<VariableDeclaration*>(); $$->push_back($decl); }
    |   function_argument_list[list] ';' function_argument_declaration[decl]     { $$ = $list; $list->push_back($decl); }
    ;

tuple_type
    :   type_declaration [typeDecl] {
        $$ = new VariableDeclaration(nullptr,$typeDecl,nullptr);
    }
    |   NAME[name] ':' type_declaration [typeDecl] {
        auto na = new vector<pair<string,LineInfo>>();
        na->push_back(make_pair(*$name,tokAt(@name)));
        $$ = new VariableDeclaration(na,$typeDecl,nullptr);
        delete $name;
    }
    ;

tuple_type_list
    :   tuple_type[decl]                               { $$ = new vector<VariableDeclaration*>(); $$->push_back($decl); }
    |   tuple_type_list[list] ';' tuple_type[decl]     { $$ = $list; $list->push_back($decl); }
    ;

variant_type
    :   NAME[name] ':' type_declaration [typeDecl] {
        auto na = new vector<pair<string,LineInfo>>();
        na->push_back(make_pair(*$name,tokAt(@name)));
        $$ = new VariableDeclaration(na,$typeDecl,nullptr);
        delete $name;
    }
    ;

variant_type_list
    :   variant_type[decl]                               { $$ = new vector<VariableDeclaration*>(); $$->push_back($decl); }
    |   variant_type_list[list] ';' variant_type[decl]   { $$ = $list; $list->push_back($decl); }
    ;


copy_or_move
    :   '='         { $$ = false; }
    |   LARROW      { $$ = true; }
    ;

variable_declaration        /* this one can have uninitialized variable which has no type */
    :   variable_name_with_pos_list[list] {
        auto autoT = new TypeDecl(Type::autoinfer);
        autoT->at = tokAt(@list);
        autoT->ref = false;
        $$ = new VariableDeclaration($list,autoT,nullptr);
    }
    |   variable_name_with_pos_list[list] '&' {
        auto autoT = new TypeDecl(Type::autoinfer);
        autoT->at = tokAt(@list);
        autoT->ref = true;
        $$ = new VariableDeclaration($list,autoT,nullptr);
    }
    |   variable_name_with_pos_list[list] ':' type_declaration[typeDecl]  {
        $$ = new VariableDeclaration($list,$typeDecl,nullptr);
    }
    |   variable_name_with_pos_list[list] ':' type_declaration[typeDecl] copy_or_move[com] expr[init] {
        $$ = new VariableDeclaration($list,$typeDecl,$init);
        $$->init_via_move = $com;
    }
    |   variable_name_with_pos_list[list] copy_or_move[com] expr[init] {
        auto typeDecl = new TypeDecl(Type::autoinfer);
        typeDecl->at = tokAt(@list);
        $$ = new VariableDeclaration($list,typeDecl,$init);
        $$->init_via_move = $com;
    }
    |   variable_name_with_pos_list[list] copy_or_move[com] expr_pipe[init] {
        auto typeDecl = new TypeDecl(Type::autoinfer);
        typeDecl->at = tokAt(@list);
        $$ = new VariableDeclaration($list,typeDecl,$init);
        $$->init_via_move = $com;
    }
    ;

copy_or_move_or_clone
    :   '='         { $$ = CorM_COPY; }
    |   LARROW      { $$ = CorM_MOVE; }
    |   CLONEEQU    { $$ = CorM_CLONE; }
    ;

optional_ref
    :       { $$ = false; }
    |   '&' { $$ = true; }
    ;

let_variable_declaration    /* let x; is prohibited. this one can't have uninitialized variable which has no type */
    :   variable_name_with_pos_list[list] ':' type_declaration[typeDecl] ';' {
        $$ = new VariableDeclaration($list,$typeDecl,nullptr);
    }
    |   variable_name_with_pos_list[list] ':' type_declaration[typeDecl] copy_or_move_or_clone[com] expr[init] ';' {
        $$ = new VariableDeclaration($list,$typeDecl,$init);
        $$->init_via_move  = ($com & CorM_MOVE) !=0;
        $$->init_via_clone = ($com & CorM_CLONE) !=0;
    }
    |   variable_name_with_pos_list[list] ':' type_declaration[typeDecl] copy_or_move_or_clone[com] expr_pipe[init] {
        $$ = new VariableDeclaration($list,$typeDecl,$init);
        $$->init_via_move  = ($com & CorM_MOVE) !=0;
        $$->init_via_clone = ($com & CorM_CLONE) !=0;
    }
    |   variable_name_with_pos_list[list] optional_ref[ref] copy_or_move_or_clone[com] expr[init] ';' {
        auto typeDecl = new TypeDecl(Type::autoinfer);
        typeDecl->at = tokAt(@list);
        typeDecl->ref = $ref;
        $$ = new VariableDeclaration($list,typeDecl,$init);
        $$->init_via_move  = ($com & CorM_MOVE) !=0;
        $$->init_via_clone = ($com & CorM_CLONE) !=0;
    }
    |   variable_name_with_pos_list[list] optional_ref[ref] copy_or_move_or_clone[com] expr_pipe[init] {
        auto typeDecl = new TypeDecl(Type::autoinfer);
        typeDecl->at = tokAt(@list);
        typeDecl->ref = $ref;
        $$ = new VariableDeclaration($list,typeDecl,$init);
        $$->init_via_move  = ($com & CorM_MOVE) !=0;
        $$->init_via_clone = ($com & CorM_CLONE) !=0;
    }
    ;

global_variable_declaration_list
    :   {
        $$ = new vector<VariableDeclaration*>();
    }
    |   global_variable_declaration_list[list] let_variable_declaration[decl] {
        $$ = $list;
        $list->push_back($decl);
    }
    ;

optional_shared
    :   { $$ = false; }
    |   DAS_SHARED  { $$ = true; }
    ;

optional_shared_or_private
    :   { $$ = 0; }
    |   DAS_SHARED  { $$ = 1; }
    |   DAS_PRIVATE  { $$ = 2; }
    ;

global_let
    :   kwd_let optional_shared_or_private[glob_shar] '{' global_variable_declaration_list[list] '}' {
        for ( auto pDecl : *$list ) {
            if ( pDecl->pTypeDecl ) {
                for ( const auto & name_at : *pDecl->pNameList ) {
                    VariablePtr pVar = make_smart<Variable>();
                    pVar->name = name_at.first;
                    pVar->at = name_at.second;
                    pVar->type = make_smart<TypeDecl>(*pDecl->pTypeDecl);
                    if ( pDecl->pInit ) {
                        pVar->init = pDecl->pInit->clone();
                        pVar->init_via_move = pDecl->init_via_move;
                        pVar->init_via_clone = pDecl->init_via_clone;
                    }
                    if ( $kwd_let ) {
                        pVar->type->constant = true;
                    } else {
                        pVar->type->removeConstant = true;
                    }
                    pVar->global_shared = ($glob_shar==1);
                    pVar->private_variable = ($glob_shar==2);
                    if ( !g_Program->addVariable(pVar) )
                        das_yyerror("global variable is already declared " + name_at.first,name_at.second,
                            CompilationError::global_variable_already_declared);
                }
            }
        }
        deleteVariableDeclarationList($list);
    }
    |   kwd_let optional_shared_or_private[glob_shar] { das_force_oxford_comma=true; } let_variable_declaration[decl] {
        auto pDecl = $decl;
        if ( pDecl->pTypeDecl ) {
            for ( const auto & name_at : *pDecl->pNameList ) {
                VariablePtr pVar = make_smart<Variable>();
                pVar->name = name_at.first;
                pVar->at = name_at.second;
                pVar->type = make_smart<TypeDecl>(*pDecl->pTypeDecl);
                if ( pDecl->pInit ) {
                    pVar->init = pDecl->pInit->clone();
                    pVar->init_via_move = pDecl->init_via_move;
                    pVar->init_via_clone = pDecl->init_via_clone;
                }
                if ( $kwd_let ) {
                    pVar->type->constant = true;
                } else {
                    pVar->type->removeConstant = true;
                }
                pVar->global_shared = ($glob_shar==1);
                pVar->private_variable = ($glob_shar==2);
                if ( !g_Program->addVariable(pVar) )
                    das_yyerror("global variable is already declared " + name_at.first,name_at.second,
                        CompilationError::global_variable_already_declared);
            }
        }
        delete pDecl;
    }
    ;

enum_list
    :   {
        $$ = new Enumeration();
    }
    |   enum_list[pE] NAME[name] ';' {
        das_checkName(*$name,tokAt(@name));
        if ( !$pE->add(*$name,nullptr,tokAt(@name)) ) {
            das_yyerror("enumeration alread declared " + *$name, tokAt(@name),
                CompilationError::enumeration_value_already_declared);
        }
        delete $name;
        $$ = $pE;
    }
    |   enum_list[pE] NAME[name] '=' expr[value] ';' {
        das_checkName(*$name,tokAt(@name));
        if ( !$pE->add(*$name,ExpressionPtr($value),tokAt(@name)) ) {
            das_yyerror("enumeration value alread declared " + *$name, tokAt(@name),
                CompilationError::enumeration_value_already_declared);
        }
        delete $name;
        $$ = $pE;
    }
    ;


single_alias
    :   NAME[name] '=' type_declaration[tdecl] ';' {
        das_checkName(*$name,tokAt(@name));
        $tdecl->alias = *$name;
        if ( !g_Program->addAlias(TypeDeclPtr($tdecl)) ) {
            das_yyerror("type alias is already defined "+*$name,tokAt(@name),
                CompilationError::type_alias_already_declared);
        }
        delete $name;
    }
    ;

alias_list
    :   single_alias
    |   alias_list single_alias
    ;

alias_declaration
    :   DAS_TYPEDEF '{' alias_list '}'
    ;

enum_declaration
    :   DAS_ENUM[loc] NAME[name] '{' enum_list[pE] '}' {
        das_checkName(*$name,tokAt(@name));
        auto pEnum = EnumerationPtr($pE);
        pEnum->at = tokAt(@name);
        pEnum->name = *$name;
        if ( !g_Program->addEnumeration(pEnum) ) {
            das_yyerror("enumeration is already defined "+*$name,tokAt(@pE),
                CompilationError::enumeration_already_declared);
        }
        delete $name;
    }
    |   DAS_ENUM[loc] NAME[name] ':' enum_basic_type_declaration[ebt] '{' enum_list[pE] '}' {
        das_checkName(*$name,tokAt(@name));
        auto pEnum = EnumerationPtr($pE);
        pEnum->at = tokAt(@name);
        pEnum->name = *$name;
        pEnum->baseType = $ebt;
        if ( !g_Program->addEnumeration(pEnum) ) {
            das_yyerror("enumeration is already defined "+*$name,tokAt(@pE),
                CompilationError::enumeration_already_declared);
        }
        delete $name;
    }
    ;

optional_structure_parent
    :                                   { $$ = nullptr; }
    |   ':' name_in_namespace[name]     { $$ = $name; }
    ;

structure_name
    : NAME[name] optional_structure_parent[parent] {
        das_checkName(*$name,tokAt(@name));
        StructurePtr pStruct;
        if ( $parent ) {
            auto structs = g_Program->findStructure(*$parent);
            if ( structs.size()==1 ) {
                pStruct = structs.back()->clone();
                pStruct->name = *$name;
                pStruct->parent = structs.back().get();
                pStruct->annotations.clear();
                pStruct->genCtor = false;
            } else if ( structs.size()==0 ) {
                das_yyerror("parent structure not found "+*$parent,tokAt(@parent),
                    CompilationError::structure_not_found);
            } else {
                string candidates = g_Program->describeCandidates(structs);
                das_yyerror("too many options for "+*$parent + "\n" + candidates,tokAt(@parent),
                    CompilationError::structure_not_found);
            }
            delete $parent;
        }
        if ( !pStruct ) {
            pStruct = make_smart<Structure>(*$name);
        }
        if ( !g_Program->addStructure(pStruct) ) {
            das_yyerror("structure is already defined "+*$name,tokAt(@name),
                CompilationError::structure_already_declared);
            $$ = nullptr;
        } else {
            $$ = pStruct.get();
            g_thisStructure = pStruct.get();
        }
        delete $name;
    }
    ;

class_or_struct
    :   DAS_CLASS   { $$ = true; }
    |   DAS_STRUCT  { $$ = false; }
    ;

structure_declaration
    : optional_annotation_list[annL] class_or_struct[loc] structure_name[ps] { if ( $ps ) $ps->isClass = $loc; } '{' struct_variable_declaration_list[list] '}' {
        if ( $ps ) {
            auto pStruct = $ps;
            pStruct->at = tokAt(@ps);
            if ( pStruct->parent && pStruct->parent->isClass != pStruct->isClass ) {
                if ( pStruct->isClass ) {
                    das_yyerror("class can only derive from class", pStruct->at,
                        CompilationError::invalid_override);
                } else {
                    das_yyerror("struct can only derive from struct", pStruct->at,
                        CompilationError::invalid_override);
                }
            }
            if ( pStruct->isClass ) {
                makeClassRtti(pStruct);
                auto virtfin = makeClassFinalize(pStruct);
                if ( !g_Program->addFunction(virtfin) ) {
                    das_yyerror("built-in finalizer is already defined " + virtfin->getMangledName(),
                        virtfin->at, CompilationError::function_already_declared);
                }
            }
            for ( auto pDecl : *$list ) {
                for ( const auto & name_at : *pDecl->pNameList ) {
                    auto oldFd = (Structure::FieldDeclaration *) pStruct->findField(name_at.first);
                    if ( !oldFd ) {
                        if ( pDecl->override ) {
                            das_yyerror("structure field is not overriding anything, "+name_at.first,name_at.second,
                                CompilationError::invalid_override);
                        } else {
                            auto td = make_smart<TypeDecl>(*pDecl->pTypeDecl);
                            auto init = pDecl->pInit ? ExpressionPtr(pDecl->pInit->clone()) : nullptr;
                            pStruct->fields.emplace_back(name_at.first, td, init,
                                pDecl->annotation ? *pDecl->annotation : AnnotationArgumentList(),
                                pDecl->init_via_move, name_at.second);
                        }
                    } else {
                        if ( pDecl->override ) {
                                auto init = pDecl->pInit ? ExpressionPtr(pDecl->pInit->clone()) : nullptr;
                                oldFd->init = init;
                                oldFd->parentType = oldFd->type->isAuto();
                        } else {
                            das_yyerror("structure field is already declared "+name_at.first
                                +", use override to replace initial value instead",name_at.second,
                                    CompilationError::invalid_override);
                        }
                    }
                }
            }
            if ( $annL ) {
                for ( auto pA : *$annL ) {
                    if ( pA->annotation ) {
                        if ( pA->annotation->rtti_isStructureAnnotation() ) {
                            auto ann = static_pointer_cast<StructureAnnotation>(pA->annotation);
                            string err;
                            if ( !ann->touch(pStruct, *g_Program->thisModuleGroup, pA->arguments, err) ) {
                                das_yyerror("can't 'touch' with structure annotation\n" + err,
                                    tokAt(@loc), CompilationError::invalid_annotation);
                            }
                        } else if ( pA->annotation->rtti_isStructureTypeAnnotation() ) {
                            if ( $annL->size()!=1 ) {
                                das_yyerror("structures are only allowed one structure type annotation", tokAt(@loc),
                                    CompilationError::invalid_annotation);
                            } else {
                                if ( !g_Program->addStructureHandle(pStruct,
                                    static_pointer_cast<StructureTypeAnnotation>(pA->annotation), pA->arguments) ) {
                                        das_yyerror("handled structure is already defined "+pStruct->name,tokAt(@loc),
                                        CompilationError::structure_already_declared);
                                } else {
                                    pStruct->module->removeStructure(pStruct);
                                }
                            }
                        }
                    }
                }
                swap ( pStruct->annotations, *$annL );
                delete $annL;
            }
        }
        deleteVariableDeclarationList($list);
        g_thisStructure = nullptr;
    }
    ;

variable_name_with_pos_list
    :   NAME[name]  {
        das_checkName(*$name,tokAt(@name));
        auto pSL = new vector<pair<string,LineInfo>>();
        pSL->push_back(make_pair(*$name,tokAt(@name)));
        $$ = pSL;
        delete $name;
    }
    |   variable_name_with_pos_list[list] ',' NAME[name] {
        das_checkName(*$name,tokAt(@name));
        $list->push_back(make_pair(*$name,tokAt(@name)));
        $$ = $list;
        delete $name;
    }
    ;

basic_type_declaration
    :   DAS_TBOOL       { $$ = Type::tBool; }
    |   DAS_TSTRING     { $$ = Type::tString; }
    |   DAS_TINT        { $$ = Type::tInt; }
    |   DAS_TINT8       { $$ = Type::tInt8; }
    |   DAS_TINT16      { $$ = Type::tInt16; }
    |   DAS_TINT64      { $$ = Type::tInt64; }
    |   DAS_TINT2       { $$ = Type::tInt2; }
    |   DAS_TINT3       { $$ = Type::tInt3; }
    |   DAS_TINT4       { $$ = Type::tInt4; }
    |   DAS_TUINT       { $$ = Type::tUInt; }
    |   DAS_TUINT8      { $$ = Type::tUInt8; }
    |   DAS_TUINT16     { $$ = Type::tUInt16; }
    |   DAS_TUINT64     { $$ = Type::tUInt64; }
    |   DAS_TUINT2      { $$ = Type::tUInt2; }
    |   DAS_TUINT3      { $$ = Type::tUInt3; }
    |   DAS_TUINT4      { $$ = Type::tUInt4; }
    |   DAS_TFLOAT      { $$ = Type::tFloat; }
    |   DAS_TFLOAT2     { $$ = Type::tFloat2; }
    |   DAS_TFLOAT3     { $$ = Type::tFloat3; }
    |   DAS_TFLOAT4     { $$ = Type::tFloat4; }
    |   DAS_TVOID       { $$ = Type::tVoid; }
    |   DAS_TRANGE      { $$ = Type::tRange; }
    |   DAS_TURANGE     { $$ = Type::tURange; }
    |   DAS_TDOUBLE     { $$ = Type::tDouble; }
    |   DAS_TBITFIELD   { $$ = Type::tBitfield; }
    ;

enum_basic_type_declaration
    :   DAS_TINT        { $$ = Type::tInt; }
    |   DAS_TINT8       { $$ = Type::tInt8; }
    |   DAS_TINT16      { $$ = Type::tInt16; }
    |   DAS_TUINT       { $$ = Type::tUInt; }
    |   DAS_TUINT8      { $$ = Type::tUInt8; }
    |   DAS_TUINT16     { $$ = Type::tUInt16; }
    ;

structure_type_declaration
    :   name_in_namespace[name]  {
        $$ = g_Program->makeTypeDeclaration(tokAt(@name),*$name);
        if ( !$$ ) {
            $$ = new TypeDecl(Type::tVoid);
            $$->at = tokAt(@name);
        }
        delete $name;
    }
    ;

auto_type_declaration
    :   DAS_TAUTO[loc] {
        $$ = new TypeDecl(Type::autoinfer);
        $$->at = tokAt(@loc);
    }
    |   DAS_TAUTO[loc] '(' NAME[alias] ')'  {
        das_checkName(*$alias,tokAt(@alias));
        $$ = new TypeDecl(Type::autoinfer);
        $$->at = tokAt(@loc);
        $$->alias = *$alias;
        delete $alias;
    }
    ;

bitfield_bits
    :   NAME[name]  {
        das_checkName(*$name,tokAt(@name));
        auto pSL = new vector<string>();
        pSL->push_back(*$name);
        $$ = pSL;
        delete $name;
    }
    |   bitfield_bits[list] ';' NAME[name] {
        das_checkName(*$name,tokAt(@name));
        $list->push_back(*$name);
        $$ = $list;
        delete $name;
    }
    ;

bitfield_type_declaration
    :   DAS_TBITFIELD[basicType] '<' { das_arrow_depth ++; } bitfield_bits[nl] '>' { das_arrow_depth --; } {
            $$ = new TypeDecl(Type::tBitfield);
            $$->argNames = *$nl;
            if ( $$->argNames.size()>32 ) {
                das_yyerror("only 32 different bits allowed",tokAt(@basicType),
                    CompilationError::invalid_type);
            }
            $$->at = tokAt(@basicType);
            delete $nl;
    }
    ;

type_declaration
    :   basic_type_declaration[basicType]                   { $$ = new TypeDecl($basicType); $$->at = tokAt(@basicType); }
    |   auto_type_declaration[typeDecl]                     { $$ = $typeDecl; }
    |   bitfield_type_declaration[typeDecl]                 { $$ = $typeDecl; }
    |   structure_type_declaration[typeDecl]                { $$ = $typeDecl; }
    |   type_declaration[typeDecl] '[' expr[dimExpr] ']' {
        int32_t dI = TypeDecl::dimConst;
        if ( $dimExpr->rtti_isConstant() ) {                // note: this shortcut is here so we don`t get extra infer pass on every array
            auto cI = (ExprConst *) $dimExpr;
            auto bt = cI->baseType;
            if ( bt==Type::tInt || bt==Type::tUInt ) {
                dI = cast<int32_t>::to(cI->value);
            }
        }
        $typeDecl->dim.push_back(dI);
        $typeDecl->dimExpr.push_back(ExpressionPtr($dimExpr));
        $typeDecl->removeDim = false;
        $$ = $typeDecl;
    }
    |   type_declaration[typeDecl] '[' ']' {
        $typeDecl->dim.push_back(TypeDecl::dimAuto);
        $typeDecl->dimExpr.push_back(nullptr);
        $typeDecl->removeDim = false;
        $$ = $typeDecl;
    }
    |   type_declaration[typeDecl] '-' '[' ']' {
        $typeDecl->removeDim = true;
        $$ = $typeDecl;
    }
    |   type_declaration[typeDecl] DAS_EXPLICIT {
        $typeDecl->isExplicit = true;
        $$ = $typeDecl;
    }
    |   type_declaration[typeDecl] DAS_CONST {
        $typeDecl->constant = true;
        $typeDecl->removeConstant = false;
        $$ = $typeDecl;
    }
    |   type_declaration[typeDecl] '-' DAS_CONST {
        $typeDecl->constant = false;
        $typeDecl->removeConstant = true;
        $$ = $typeDecl;
    }
    |   type_declaration[typeDecl] '&' {
        $typeDecl->ref = true;
        $typeDecl->removeRef = false;
        $$ = $typeDecl;
    }
    |   type_declaration[typeDecl] '-' '&' {
        $typeDecl->ref = false;
        $typeDecl->removeRef = true;
        $$ = $typeDecl;
    }
    |   type_declaration[typeDecl] '#' {
        $$ = $typeDecl;
        $$->temporary = true;
    }
    |   type_declaration[typeDecl] DAS_IMPLICIT {
        $$ = $typeDecl;
        $$->implicit = true;
    }
    |   type_declaration[typeDecl] '-' '#' {
        $typeDecl->temporary = false;
        $typeDecl->removeTemporary = true;
        $$ = $typeDecl;
    }
    |   type_declaration[typeDecl] '=' DAS_CONST {
        $typeDecl->explicitConst = true;
        $$ = $typeDecl;
    }
    |   type_declaration[typeDecl] '?' {
        $$ = new TypeDecl(Type::tPointer);
        $$->at = tokAt(@typeDecl);
        $$->firstType = TypeDeclPtr($typeDecl);
    }
    |   DAS_SMART_PTR[loc] '<' { das_arrow_depth ++; } type_declaration[typeDecl] '>' { das_arrow_depth --; } {
        $$ = new TypeDecl(Type::tPointer);
        $$->at = tokAt(@loc);
        $$->smartPtr = true;
        $$->firstType = TypeDeclPtr($typeDecl);
    }
    |   type_declaration[typeDecl] QQ {
        $$ = new TypeDecl(Type::tPointer);
        $$->at = tokAt(@typeDecl);
        $$->firstType = make_smart<TypeDecl>(Type::tPointer);
        $$->firstType->at = tokAt(@typeDecl);
        $$->firstType->firstType = TypeDeclPtr($typeDecl);
    }
    |   DAS_ARRAY[loc] '<' { das_arrow_depth ++; } type_declaration[typeDecl] '>' { das_arrow_depth --; } {
        $$ = new TypeDecl(Type::tArray);
        $$->at = tokAt(@loc);
        $$->firstType = TypeDeclPtr($typeDecl);
    }
    |   DAS_TABLE[loc] '<' { das_arrow_depth ++; } type_declaration[keyTypeDecl] ';' type_declaration[valueTypeDecl] '>' { das_arrow_depth --; } {
        $$ = new TypeDecl(Type::tTable);
        $$->at = tokAt(@loc);
        $$->firstType = TypeDeclPtr($keyTypeDecl);
        $$->secondType = TypeDeclPtr($valueTypeDecl);
    }
    |   DAS_ITERATOR[loc] '<'  { das_arrow_depth ++; } type_declaration[itTypeDecl] '>' { das_arrow_depth --; } {
        $$ = new TypeDecl(Type::tIterator);
        $$->at = tokAt(@loc);
        $$->firstType = TypeDeclPtr($itTypeDecl);
    }
    |   DAS_TBLOCK[loc] {
        $$ = new TypeDecl(Type::tBlock);
        $$->at = tokAt(@loc);
    }
    |   DAS_TBLOCK[loc] '<'  { das_arrow_depth ++; } type_declaration[blockType] '>' { das_arrow_depth --; } {
        $$ = new TypeDecl(Type::tBlock);
        $$->at = tokAt(@loc);
        $$->firstType = TypeDeclPtr($blockType);
    }
    |   DAS_TBLOCK[loc] '<'  { das_arrow_depth ++; } optional_function_argument_list[list] optional_function_type[result] '>' { das_arrow_depth --; } {
        $$ = new TypeDecl(Type::tBlock);
        $$->at = tokAt(@loc);
        $$->firstType = TypeDeclPtr($result);
        if ( $list ) {
            varDeclToTypeDecl($$, $list);
            deleteVariableDeclarationList($list);
        }
    }
    |   DAS_TFUNCTION[loc] {
        $$ = new TypeDecl(Type::tFunction);
        $$->at = tokAt(@loc);
    }
    |   DAS_TFUNCTION[loc] '<' { das_arrow_depth ++; } type_declaration[blockType] '>' { das_arrow_depth --; } {
        $$ = new TypeDecl(Type::tFunction);
        $$->at = tokAt(@loc);
        $$->firstType = TypeDeclPtr($blockType);
    }
    |   DAS_TFUNCTION[loc] '<' { das_arrow_depth ++; } optional_function_argument_list[list] optional_function_type[result] '>' { das_arrow_depth --; } {
        $$ = new TypeDecl(Type::tFunction);
        $$->at = tokAt(@loc);
        $$->firstType = TypeDeclPtr($result);
        if ( $list ) {
            varDeclToTypeDecl($$, $list);
            deleteVariableDeclarationList($list);
        }
    }
    |   DAS_TLAMBDA[loc] {
        $$ = new TypeDecl(Type::tLambda);
        $$->at = tokAt(@loc);
    }
    |   DAS_TLAMBDA[loc] '<' { das_arrow_depth ++; } type_declaration[blockType] '>' { das_arrow_depth --; } {
        $$ = new TypeDecl(Type::tLambda);
        $$->at = tokAt(@loc);
        $$->firstType = TypeDeclPtr($blockType);
    }
    |   DAS_TLAMBDA[loc] '<' { das_arrow_depth ++; } optional_function_argument_list[list] optional_function_type[result] '>' { das_arrow_depth --; } {
        $$ = new TypeDecl(Type::tLambda);
        $$->at = tokAt(@loc);
        $$->firstType = TypeDeclPtr($result);
        if ( $list ) {
            varDeclToTypeDecl($$, $list);
            deleteVariableDeclarationList($list);
        }
    }
    |   DAS_TTUPLE[loc] '<' { das_arrow_depth ++; } tuple_type_list[list] '>' { das_arrow_depth --; } {
        $$ = new TypeDecl(Type::tTuple);
        $$->at = tokAt(@loc);
        varDeclToTypeDecl($$, $list, true);
        deleteVariableDeclarationList($list);
    }
    |   DAS_TVARIANT[loc] '<' { das_arrow_depth ++; } variant_type_list[list] '>' { das_arrow_depth --; } {
        $$ = new TypeDecl(Type::tVariant);
        $$->at = tokAt(@loc);
        varDeclToTypeDecl($$, $list, true);
        deleteVariableDeclarationList($list);
    }
    ;

variant_alias_declaration
    :   DAS_TVARIANT { das_need_oxford_comma=false; } NAME[vname] '{' variant_type_list[list] ';' '}' {
        auto vtype = make_smart<TypeDecl>(Type::tVariant);
        vtype->alias = *$vname;
        vtype->at = tokAt(@vname);
        varDeclToTypeDecl(vtype.get(), $list, true);
        deleteVariableDeclarationList($list);
        if ( !g_Program->addAlias(vtype) ) {
            das_yyerror("type alias is already defined "+*$vname,tokAt(@vname),
                CompilationError::type_alias_already_declared);
        }
        delete $vname;
    }
    ;

bitfield_alias_declaration
    :   DAS_TBITFIELD { das_need_oxford_comma=false; } NAME[vname] '{' bitfield_bits[list] ';' '}' {
        auto btype = make_smart<TypeDecl>(Type::tBitfield);
        btype->alias = *$vname;
        btype->at = tokAt(@vname);
        btype->argNames = *$list;
        if ( btype->argNames.size()>32 ) {
            das_yyerror("only 32 different bits allowed",tokAt(@vname),
                CompilationError::invalid_type);
        }
        if ( !g_Program->addAlias(btype) ) {
            das_yyerror("type alias is already defined "+*$vname,tokAt(@vname),
                CompilationError::type_alias_already_declared);
        }
        delete $vname;
        delete $list;
    }
    ;


make_decl
    :   make_struct_decl[msd]    { $$ = $msd; }
    |   make_dim_decl[mdd]       { $$ = $mdd; }
    |   make_table_decl[mtd]     { $$ = $mtd; }
    |   array_comprehension[mac] { $$ = $mac; }
    ;

make_struct_fields
    :   NAME[fn] copy_or_move[com] expr[value] {
        auto mfd = make_smart<MakeFieldDecl>(tokAt(@fn),*$fn,ExpressionPtr($value),$com,false);
        delete $fn;
        auto msd = new MakeStruct();
        msd->push_back(mfd);
        $$ = msd;
    }
    |   NAME[fn] CLONEEQU expr[value] {
        auto mfd = make_smart<MakeFieldDecl>(tokAt(@fn),*$fn,ExpressionPtr($value),false,true);
        delete $fn;
        auto msd = new MakeStruct();
        msd->push_back(mfd);
        $$ = msd;
    }
    |   make_struct_fields[msd] ',' NAME[fn] copy_or_move[com] expr[value] {
        auto mfd = make_smart<MakeFieldDecl>(tokAt(@fn),*$fn,ExpressionPtr($value),$com,false);
        delete $fn;
        ((MakeStruct *)$msd)->push_back(mfd);
        $$ = $msd;
    }
    |   make_struct_fields[msd] ',' NAME[fn] CLONEEQU expr[value] {
        auto mfd = make_smart<MakeFieldDecl>(tokAt(@fn),*$fn,ExpressionPtr($value),false,true);
        delete $fn;
        ((MakeStruct *)$msd)->push_back(mfd);
        $$ = $msd;
    }
    ;

make_struct_dim
    :   make_struct_fields[msf] {
        auto msd = new ExprMakeStruct();
        msd->structs.push_back(MakeStructPtr($msf));
        $$ = msd;
    }
    |   make_struct_dim[msd] ';' make_struct_fields[msf] {
        ((ExprMakeStruct *) $msd)->structs.push_back(MakeStructPtr($msf));
        $$ = $msd;
    }
    ;

optional_block
    :   { $$ = nullptr; }
    |   DAS_WHERE expr_block[blk] { $$ = $blk; }
    ;

make_struct_decl
    :   BRABRAB [loc] type_declaration[mkt] make_struct_dim[msd] optional_block[blk] ']' ']' {
        ((ExprMakeStruct *)$msd)->makeType = TypeDeclPtr($mkt);
        ((ExprMakeStruct *)$msd)->block = $blk;
        $msd->at = tokAt(@loc);
        $$ = $msd;
    }
    |   BRABRAB [loc] type_declaration[mkt] optional_block[blk] ']' ']' {
        auto msd = new ExprMakeStruct();
        msd->makeType = TypeDeclPtr($mkt);
        msd->block = $blk;
        msd->at = tokAt(@loc);
        $$ = msd;
    }
    |   BRABRAB [loc] type_declaration[mkt] '(' ')' optional_block[blk] ']' ']' {
        auto msd = new ExprMakeStruct();
        msd->makeType = TypeDeclPtr($mkt);
        msd->useInitializer = true;
        msd->block = $blk;
        msd->at = tokAt(@loc);
        $$ = msd;
    }
    |   BRABRAB [loc] type_declaration[mkt] '(' ')' make_struct_dim[msd] optional_block[blk] ']' ']' {
        ((ExprMakeStruct *)$msd)->makeType = TypeDeclPtr($mkt);
        ((ExprMakeStruct *)$msd)->useInitializer = true;
        ((ExprMakeStruct *)$msd)->block = $blk;
        $msd->at = tokAt(@loc);
        $$ = $msd;
    }
    |   BRACBRB [loc] type_declaration[mkt] make_struct_dim[msd] optional_block[blk] '}' ']' {
        ((ExprMakeStruct *)$msd)->makeType = TypeDeclPtr($mkt);
        ((ExprMakeStruct *)$msd)->block = $blk;
        $msd->at = tokAt(@loc);
        auto tam = g_Program->makeCall(tokAt(@loc),"to_array_move");
        tam->arguments.push_back(ExpressionPtr($msd));
        $$ = tam;
    }
    |   BRACBRB [loc] type_declaration[mkt] '(' ')' make_struct_dim[msd] optional_block[blk] '}' ']' {
        ((ExprMakeStruct *)$msd)->makeType = TypeDeclPtr($mkt);
        ((ExprMakeStruct *)$msd)->useInitializer = true;
        ((ExprMakeStruct *)$msd)->block = $blk;
        $msd->at = tokAt(@loc);
        auto tam = g_Program->makeCall(tokAt(@loc),"to_array_move");
        tam->arguments.push_back(ExpressionPtr($msd));
        $$ = tam;
    }
    ;

make_tuple
    :   expr[val] {
        $$ = $val;
    }
    |   make_tuple[mkt] ',' expr[val] {
        ExprMakeTuple * mt;
        if ( $mkt->rtti_isMakeTuple() ) {
            mt = static_cast<ExprMakeTuple *>($mkt);
        } else {
            mt = new ExprMakeTuple(tokAt(@mkt));
            mt->values.push_back(ExpressionPtr($mkt));
        }
        mt->values.push_back(ExpressionPtr($val));
        $$ = mt;
    }
    ;

make_map_tuple
    :   expr[kval] MAPTO[mkt] expr[vval] {
        ExprMakeTuple * mt = new ExprMakeTuple(tokAt(@mkt));
        mt->values.push_back(ExpressionPtr($kval));
        mt->values.push_back(ExpressionPtr($vval));
        $$ = mt;
    }
    ;

make_any_tuple
    :   make_tuple[mkt]         { $$ = $mkt; }
    |   make_map_tuple[mkm]     { $$ = $mkm; }
    ;

make_dim
    :   make_any_tuple[val] {
        auto mka = new ExprMakeArray();
        mka->values.push_back(ExpressionPtr($val));
        $$ = mka;
    }
    |   make_dim[mka] ';' make_any_tuple[val] {
        ((ExprMakeArray *) $mka)->values.push_back(ExpressionPtr($val));
        $$ = $mka;
    }
    ;

make_dim_decl
    :   BRABRAB [loc] type_declaration[mkt] make_dim[mka] ']' ']' {
       ((ExprMakeArray *)$mka)->makeType = TypeDeclPtr($mkt);
       $mka->at = tokAt(@loc);
       $$ = $mka;
    }
    |   BRACBRB [loc] type_declaration[mkt] make_dim[mka] '}' ']' {
       ((ExprMakeArray *)$mka)->makeType = TypeDeclPtr($mkt);
       $mka->at = tokAt(@loc);
       auto tam = g_Program->makeCall(tokAt(@loc),"to_array_move");
       tam->arguments.push_back(ExpressionPtr($mka));
       $$ = tam;
    }
    ;

make_table
    :   make_map_tuple[val] {
        auto mka = new ExprMakeArray();
        mka->values.push_back(ExpressionPtr($val));
        $$ = mka;
    }
    |   make_table[mka] ';' make_map_tuple[val] {
        ((ExprMakeArray *) $mka)->values.push_back(ExpressionPtr($val));
        $$ = $mka;
    }
    ;

make_table_decl
    :   CBRCBRB [loc] make_table[mka] '}' '}' {
        auto mkt = make_smart<TypeDecl>(Type::autoinfer);
        mkt->dim.push_back(TypeDecl::dimAuto);
        ((ExprMakeArray *)$mka)->makeType = mkt;
        $mka->at = tokAt(@loc);
        auto ttm = g_Program->makeCall(tokAt(@loc),"to_table_move");
        ttm->arguments.push_back(ExpressionPtr($mka));
        $$ = ttm;
    }
    ;

array_comprehension_where
    :                               { $$ = nullptr; }
    |   ';' DAS_WHERE expr[clause]  { $$ = $clause; }
    ;

array_comprehension
    :   BRABRAB DAS_FOR[loc] variable_name_with_pos_list[iters] DAS_IN expr_list[srcs]  ';' expr[subexpr] array_comprehension_where[where] ']' ']' [forend] {
        auto pFor = make_smart<ExprFor>(tokAt(@loc));
        pFor->visibility = tokRangeAt(@loc,@forend);
        for ( const auto & np : *$iters ) {
            pFor->iterators.push_back(np.first);
            pFor->iteratorsAt.push_back(np.second);
        }
        delete $iters;
        pFor->sources = sequenceToList($srcs);
        auto pAC = new ExprArrayComprehension(tokAt(@loc));
        pAC->generatorSyntax = true;
        pAC->exprFor = pFor;
        pAC->subexpr = ExpressionPtr($subexpr);
        if ( $where ) {
            pAC->exprWhere = ExpressionPtr($where);
        }
        $$ = pAC;
    }
    |   BRACBRB DAS_FOR[loc] variable_name_with_pos_list[iters] DAS_IN expr_list[srcs]  ';' expr[subexpr] array_comprehension_where[where] '}' ']' [forend] {
        auto pFor = make_smart<ExprFor>(tokAt(@loc));
        pFor->visibility = tokRangeAt(@loc,@forend);
        for ( const auto & np : *$iters ) {
            pFor->iterators.push_back(np.first);
            pFor->iteratorsAt.push_back(np.second);
        }
        delete $iters;
        pFor->sources = sequenceToList($srcs);
        auto pAC = new ExprArrayComprehension(tokAt(@loc));
        pAC->generatorSyntax = false;
        pAC->exprFor = pFor;
        pAC->subexpr = ExpressionPtr($subexpr);
        if ( $where ) {
            pAC->exprWhere = ExpressionPtr($where);
        }
        $$ = pAC;
    }
    ;

%%

void das_checkName(const string & name, const LineInfo &at) {
    if ( name.length()>=2 && name[0]=='_' && name[1]=='_' ) {
        g_Program->error("names starting with __ are reserved, " + name,"","",at,CompilationError::invalid_name);
    }
}

void das_yyerror(const string & error, const LineInfo & at, CompilationError cerr) {
    g_Program->error(error,"","",at,cerr);
}

void das_yyfatalerror(const string & error, CompilationError cerr) {
    g_Program->error(error,"","",LineInfo(g_FileAccessStack.back(),
        yylloc.first_column,yylloc.first_line,yylloc.last_column,yylloc.last_line),cerr);
}

void das_yyerror(const string & error) {
    if ( !das_supress_errors ) {
        g_Program->error(error,"","",LineInfo(g_FileAccessStack.back(),
            yylloc.first_column,yylloc.first_line,yylloc.last_column,yylloc.last_line),
                CompilationError::syntax_error);
    }
}

vector<ExpressionPtr> sequenceToList ( Expression * arguments ) {
    vector<ExpressionPtr> argList;
    auto arg = arguments;
    if ( arg->rtti_isSequence() ) {
        while ( arg->rtti_isSequence() ) {
            auto pSeq = static_cast<ExprSequence *>(arg);
            DAS_ASSERT(!pSeq->right->rtti_isSequence());
            argList.push_back(pSeq->right);
            arg = pSeq->left.get();
        }
        argList.push_back(arg);
        reverse(argList.begin(),argList.end());
        delete arguments;
    } else {
        argList.push_back(ExpressionPtr(arg));
    }
    return argList;
}

ExprLooksLikeCall * parseFunctionArguments ( ExprLooksLikeCall * pCall, Expression * arguments ) {
    pCall->arguments = sequenceToList(arguments);
    return pCall;
}

void deleteVariableDeclarationList ( vector<VariableDeclaration *> * list ) {
    if ( !list ) return;
    for ( auto pD : *list )
        delete pD;
    delete list;
}

LineInfo tokAt ( const struct YYLTYPE & li ) {
    return LineInfo(g_FileAccessStack.back(),
        li.first_column,li.first_line,
        li.last_column,li.last_line);
}

LineInfo tokRangeAt ( const struct YYLTYPE & li, const struct YYLTYPE & lie ) {
    return LineInfo(g_FileAccessStack.back(),
        li.first_column,li.first_line,
        lie.last_column,lie.last_line);
}

Annotation * findAnnotation ( const string & name, const LineInfo & at ) {
    auto ann = g_Program->findAnnotation(name);
    if ( ann.size()==1 ) {
        return ann.back().get();
    } else if ( ann.size()==0 ) {
        das_yyerror("annotation " + name + " not found", at, CompilationError::annotation_not_found );
        return nullptr;
    } else {
        string candidates = g_Program->describeCandidates(ann);
        das_yyerror("too many options for annotation " + name + "\n" + candidates, at, CompilationError::annotation_not_found );
        return nullptr;
    }
}

void varDeclToTypeDecl ( TypeDecl * pType, vector<VariableDeclaration*> * list, bool needNames ) {
    bool anyNames = false;
    for ( auto pDecl : *list ) {
        if ( pDecl->pTypeDecl ) {
            int count = pDecl->pNameList ? int(pDecl->pNameList->size()) : 1;
            for ( int ai=0; ai!=count; ++ai ) {
                auto pVarType = make_smart<TypeDecl>(*pDecl->pTypeDecl);
                if ( pDecl->pInit ) {
                    das_yyerror("can't have default values in type declaration",
                        (*pDecl->pNameList)[ai].second,CompilationError::cant_initialize);
                }
                pType->argTypes.push_back(pVarType);
                if ( needNames && pDecl->pNameList && !(*pDecl->pNameList)[ai].first.empty() ) {
                    anyNames = true;
                }
            }
        }
    }
    if ( anyNames ) {
        for ( auto pDecl : *list ) {
            if ( pDecl->pTypeDecl ) {
                if ( pDecl->pNameList ) {
                    for ( const auto & name : *pDecl->pNameList ) {
                        pType->argNames.push_back(name.first);
                    }
                } else {
                    pType->argNames.push_back(string());
                }
            }
        }
    }
}

void runFunctionAnnotations ( Function * func, AnnotationList * annL, const LineInfo & at ) {
    if ( annL ) {
        for ( auto pA : *annL ) {
            if ( pA->annotation ) {
                if ( pA->annotation->rtti_isFunctionAnnotation() ) {
                    auto ann = static_pointer_cast<FunctionAnnotation>(pA->annotation);
                    string err;
                    if ( !ann->apply(func, *g_Program->thisModuleGroup, pA->arguments, err) ) {
                        das_yyerror("can't apply annotation\n" + err,
                            at, CompilationError::invalid_annotation);
                    }
                } else {
                    das_yyerror("functions are only allowed function annotations",
                        at, CompilationError::invalid_annotation);
                }
            }
        }
        swap ( func->annotations, *annL );
        delete annL;
    }
}
